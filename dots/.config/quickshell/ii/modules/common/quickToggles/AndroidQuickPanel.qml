import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Bluetooth

import qs.modules.common.quickToggles.androidStyle
import "androidStyle/QuickToggleCatalog.js" as QuickToggleCatalog
import "androidStyle/QuickToggleLayout.js" as QuickToggleLayout

AbstractQuickPanel {
    id: root
    property bool editMode: false
    // Full-screen hosts can own the vertical axis for the complete panel. In that mode the
    // unused-toggle tray publishes its natural height and never steals a drag from the
    // surrounding Flickable; horizontal paging remains local to this component.
    property bool externalVerticalScroll: false
    // Gesture-driven hosts feed their 0→1 pull progress here so the sliders and the tile grid
    // come in one after the other instead of appearing at once. 1.0 = fully revealed (ii).
    property real revealProgress: 1.0
    function stageReveal(delay) {
        const span = Math.max(0.001, 1 - delay);
        return Math.max(0, Math.min(1, (root.revealProgress - delay) / span));
    }
    Layout.fillWidth: true
    Layout.alignment: Qt.AlignHCenter

    // Current page index
    property int currentPage: 0

    property int entranceTrigger: -1

    Connections {
        target: GlobalStates
        function onSidebarRightOpenChanged() {
            if (!GlobalStates.sidebarRightOpen && editController.active) {
                editController.cancel();
            }
        }
    }

    onEditModeChanged: {
        if (!root.editMode && editController.active)
            editController.cancel();
    }

    Shortcut {
        sequence: "Escape"
        enabled: root.editMode && editController.active
        onActivated: editController.cancel()
    }

    // Sizes
    property real spacing: 6
    property real padding: 6
readonly property real baseCellWidth: {
    const badgePad = root.trayBadgeOverhang + 2;
    const availableWidth = root.width - (root.padding * 2)
        - (root.spacing * Math.max(0, root.columns - 1))
        - (badgePad * 2);
    return Math.max(1, availableWidth / Math.max(1, root.columns));
}
    readonly property real gridWidth: Math.max(0, (root.columns * root.baseCellWidth) + (Math.max(0, root.columns - 1) * root.spacing))
    // Hosts with touch-sized grids (tablet family) raise this; the ii sidebar keeps 56,
    // and every derived metric (icon circles, typography) scales off it.
    property real baseCellHeight: 56

    // Toggles config
    readonly property list<string> availableToggleTypes: QuickToggleCatalog.allTypes()
    function isToggleVisible(toggleType) {
        return QuickToggleCatalog.availableForFamily(toggleType, PanelFamily.current)
    }
    /**
     * The layout object this family owns, and the one every edit writes to.
     *
     * Not `quickToggles.android` directly any more: the desktop sidebar is 460px wide and
     * the tablet's shade is the whole screen, so one arrangement cannot serve both — and
     * sharing the key meant adapting either silently rearranged the other. See
     * PanelFamily.quickToggleLayout.
     *
     * `layoutEntity` points an instance at a specific storage bucket; empty keeps the
     * family default (sidebar/tablet), "shelf" uses the ake bono popup's own arrangement.
     */
    property string layoutEntity: ""
    // Whether 3-way slider-capable toggles render as drag sliders. The family
    // default follows the sidebar's setting; the ake bono shelf popup overrides
    // this with its own key (`akebono.shelf.quickSettings.useThreeWaySliders`) so
    // the popup isn't governed by a main-bar preference. Delegates read it off the
    // panel instead of Config directly for the same reason.
    property bool useThreeWaySliders: Config.options.sidebar.quickToggles.useThreeWaySliders
    // Which fixed-slider row the panel draws on top of the grid. The sidebar panel
    // owns the main bar's `sidebar.quickSliders`; the ake bono shelf popup injects
    // its own `akebono.shelf.quickSliders` so the two never fight over the keys.
    property var quickSlidersConfig: Config.options.sidebar.quickSliders
    readonly property var layoutConfig: root.layoutEntity !== ""
        ? PanelFamily.quickToggleLayout(root.layoutEntity)
        : PanelFamily.quickToggleLayout()

    readonly property int columns: root.layoutConfig?.columns ?? 4

    // Pages data — reads from Config and exposes the canonical in-memory shape.
    // The legacy `size` field is read only by the catalog normalizer and is not
    // returned to delegates.
    readonly property list<var> pages: {
        if (!Config.ready)
            return [[]];
        // Not `layoutConfig.pages`: a family that has never been edited borrows the
        // desktop's arrangement rather than opening on a blank grid.
        const stored = PanelFamily.quickTogglePages(root.layoutEntity);
        if (!stored || stored.length === 0)
            return [[]];
        return QuickToggleCatalog.normalizePages(stored, root.columns, {
            warn: function(message) { console.warn(message); }
        });
    }

    QuickToggleEditController {
        id: editController
        config: root.layoutConfig
        persistedPages: root.pages
        columns: root.columns
        // Hold a fresh swap for exactly as long as the delegates take to slide
        // into their new slots, so a hesitating pointer cannot re-order the
        // grid while it is still visibly reflowing. Zero when animations are
        // off, because then there is nothing to wait for.
        reorderSettleMs: Appearance.animation.elementMoveFast.duration
    }

    property alias editController: editController

    // The persisted page arrays are the delegate model. A gesture may change
    // preview geometry, but it must never reorder/retype this model while a
    // MouseArea owns the grab.
    readonly property list<var> displayPages: root.pages

    // Same-page reorder and resize get a live packed preview. Cross-page drag
    // keeps both pages stable until release, then commits one atomic move.
    readonly property list<var> geometryPages: {
        if (!editController.active)
            return root.pages;
        if (editController.mode === "resize"
                || editController.targetPage === editController.sourcePage)
            return editController.draftPages;
        return root.pages;
    }

    // All used toggle types across all pages
    readonly property list<string> allUsedTypes: {
        var types = [];
        for (var p = 0; p < root.pages.length; p++) {
            var page = root.pages[p];
            if (!page)
                continue;
            for (var i = 0; i < page.length; i++) {
                if (page[i] && page[i].type)
                    types.push(page[i].type);
            }
        }
        return types;
    }

    readonly property list<var> unusedToggles: {
        const types = availableToggleTypes.filter(type => root.isToggleVisible(type) && !allUsedTypes.includes(type));
        return types.map(type => QuickToggleCatalog.item(type, type, undefined, undefined, root.columns));
    }

    readonly property var packedUnusedToggles: QuickToggleLayout.pack(root.unusedToggles, root.columns)
    readonly property list<var> positionedUnusedToggles: QuickToggleLayout.positionedItems(
        root.unusedToggles,
        root.packedUnusedToggles,
        root.baseCellWidth,
        root.baseCellHeight,
        root.spacing
    )

    // One packer owns both visible geometry and height. Delegates are decorated
    // by stable id below; their model order remains the persisted order.
    readonly property list<var> packedPages: {
        var result = [];
        for (var i = 0; i < geometryPages.length; i++)
            result.push(QuickToggleLayout.pack(geometryPages[i] || [], root.columns));
        return result;
    }

    readonly property list<var> positionedPages: {
        var result = [];
        for (var i = 0; i < root.pages.length; i++) {
            result.push(QuickToggleLayout.positionedItems(
                root.pages[i] || [],
                root.packedPages[i] || { rowsUsed: 0, items: [] },
                root.baseCellWidth,
                root.baseCellHeight,
                root.spacing
            ));
        }
        return result;
    }

    // Calculate height for a specific page
    function pageHeight(pageIndex) {
        if (pageIndex < 0 || pageIndex >= root.pages.length)
            return baseCellHeight + 8;
        var packedPage = packedPages[pageIndex];
        var rows = packedPage ? packedPage.rowsUsed : 0;
        return Math.max(baseCellHeight, rows * (baseCellHeight + spacing) - spacing) + 8;
    }

    // Dynamic height based on current page + page indicators
    readonly property real currentContentHeight: pageHeight(currentPage) + (editMode ? 14 : 0)

    // How tall the panel is allowed to get, handed down by whoever hosts it.
    // Negative means unconstrained, which is what a host that does not measure
    // itself gets. Everything above the tray is fixed, so the tray gets what is
    // left of the budget and scrolls the rest.
    property real maxContentHeight: -1
    // Every unused toggle wears an add badge that hangs 6px past its own top and
    // right edge (EditableQuickToggleItem). Outside a clip that just draws over
    // the panel padding; inside one it gets sliced, so the tray has to hand those
    // 6px back on both sides.
    readonly property real trayBadgeOverhang: 6
    readonly property real trayMaxHeight: {
        if (root.maxContentHeight <= 0)
            return -1;
        return Math.max(root.baseCellHeight,
            root.maxContentHeight - unusedTogglesLoader.y - root.padding * 2);
    }

    implicitHeight: contentItem.implicitHeight + root.padding * 2

    // Page management functions
    function addPage() {
        if (editController.addPage())
            currentPage = editController.targetPage;
    }

    function removePage(pageIndex) {
        if (!editController.removePage(pageIndex))
            return;
        var remaining = root.pages.length;
        currentPage = Math.min(currentPage, Math.max(0, remaining - 1));
    }

    function goToPage(pageIndex) {
        if (pageIndex < 0 || pageIndex >= displayPages.length)
            return;
        currentPage = pageIndex;
    }

    // Drag-scroll: called by toggle buttons during drag to auto-scroll pages
    // absX: x coordinate mapped to panel root
    // dragButton: the toggle button being dragged
    property real dragScrollEdgeThreshold: 40
    property int dragScrollPendingPage: -1

    Timer {
        id: dragScrollTimer
        interval: 500
        repeat: false
        onTriggered: {
            if (root.dragScrollPendingPage >= 0 && root.dragScrollPendingPage < root.displayPages.length) {
                root.currentPage = root.dragScrollPendingPage;
                if (root.editController.active)
                    root.editController.setTargetPage(root.dragScrollPendingPage);
            }
            root.dragScrollPendingPage = -1;
        }
    }

    function cancelDragScroll() {
        dragScrollTimer.stop();
        dragScrollPendingPage = -1;
    }

    function handleDragScrollRequest(absX, dragButton) {
        var newPage = -1;
        if (absX < dragScrollEdgeThreshold && currentPage > 0) {
            newPage = currentPage - 1;
        } else if (absX > root.width - dragScrollEdgeThreshold && currentPage < displayPages.length - 1) {
            newPage = currentPage + 1;
        }

        if (newPage >= 0 && newPage !== dragScrollPendingPage) {
            dragScrollPendingPage = newPage;
            dragScrollTimer.restart();
        } else if (newPage < 0) {
            // Back in safe zone — reset pending
            dragScrollPendingPage = -1;
            dragScrollTimer.stop();
        }
    }

    Column {
        id: contentItem
        anchors {
            left: parent.left
            right: parent.right
            top: parent.top
            margins: root.padding
        }
        spacing: 8

        Column {
            id: fixedSlidersColumn
            anchors.horizontalCenter: parent.horizontalCenter
            width: root.gridWidth
            spacing: root.spacing

            readonly property real reveal: root.stageReveal(0)
            opacity: reveal
            transform: Translate {
                y: -(1 - fixedSlidersColumn.reveal) * root.baseCellHeight * 0.5
            }

            StableQuickToggleModel {
                id: fixedSlidersModel
                sourceValues: {
                    var list = [];
                    const cfg = root.quickSlidersConfig;
                    if (cfg.enable) {
                        if (cfg.showBrightness)
                            list.push(QuickToggleCatalog.item("brightnessSlider", "brightnessSlider", root.columns, 1, root.columns));
                        if (cfg.showGamma)
                            list.push(QuickToggleCatalog.item("gammaSlider", "gammaSlider", root.columns, 1, root.columns));
                        if (cfg.showVolume)
                            list.push(QuickToggleCatalog.item("volumeSlider", "volumeSlider", root.columns, 1, root.columns));
                        if (cfg.showMic)
                            list.push(QuickToggleCatalog.item("micSlider", "micSlider", root.columns, 1, root.columns));
                    }
                    return list;
                }
            }

            Repeater {
                id: fixedSlidersRepeater
                model: fixedSlidersModel
                delegate: AndroidToggleDelegateChooser {
                    editMode: false // Force false so they can't be dragged
                    baseCellWidth: root.baseCellWidth
                    baseCellHeight: root.baseCellHeight
                    spacing: root.spacing
                    isUnused: false
                    pageIndex: -1
                    gridColumns: root.columns
                    panel: root
                    gridRef: fixedSlidersColumn
                    entranceTrigger: root.entranceTrigger

                    onOpenAudioOutputDialog: root.openAudioOutputDialog()
                    onOpenAudioInputDialog: root.openAudioInputDialog()
                    onOpenBluetoothDialog: root.openBluetoothDialog()
                    onOpenNightLightDialog: root.openNightLightDialog()
                    onOpenWifiDialog: root.openWifiDialog()
                    onOpenDarkModeDialog: root.openDarkModeDialog()
                    onOpenLocalSendDialog: root.openLocalSendDialog()
                    onOpenVpnDialog: root.openVpnDialog()
                    onOpenTailscaleDialog: root.openTailscaleDialog()
                    onOpenDnsOverTlsDialog: root.openDnsOverTlsDialog()
                    onOpenIdleInhibitorDialog: root.openIdleInhibitorDialog()
                    onOpenScreenShaderDialog: root.openScreenShaderDialog()
                    onOpenModesDialog: root.openModesDialog()
                }
            }
        }

        // Horizontal paging container
        Item {
            id: flickableContainer
            width: parent.width
            height: root.currentContentHeight

            // Morph smoothly when another page is bigger/smaller than this one
            Behavior on height {
                NumberAnimation {
                    duration: 240
                    easing.type: Easing.OutQuint
                }
            }

            readonly property real reveal: root.stageReveal(0.25)
            opacity: reveal
            transform: Translate {
                y: -(1 - flickableContainer.reveal) * root.baseCellHeight * 0.7
            }

            // The grid sits inside the panel padding, which is at least the badge
            // overhang wide, so edit badges stay inside and neighbours stay hidden.
            clip: true

            Flickable {
                id: flickable
                anchors.fill: parent
                contentWidth: width * root.displayPages.length
                contentHeight: height
                flickableDirection: Flickable.HorizontalFlick
                boundsBehavior: Flickable.StopAtBounds
                interactive: !root.editMode

                // Snap to page on release
                onMovementEnded: {
                    var targetPage = Math.round(contentX / width);
                    targetPage = Math.max(0, Math.min(targetPage, root.displayPages.length - 1));
                    root.currentPage = targetPage;
                    snapAnimation.to = targetPage * width;
                    snapAnimation.start();
                }

                // Mouse wheel / scroll paging
                MouseArea {
                    anchors.fill: parent
                    acceptedButtons: Qt.NoButton
                    onWheel: function (wheelEvent) {
                        if (root.externalVerticalScroll
                                && Math.abs(wheelEvent.angleDelta.y) >= Math.abs(wheelEvent.angleDelta.x)) {
                            // Let the host's vertical WheelHandler move the complete column,
                            // from configured toggles all the way through the unused tray.
                            wheelEvent.accepted = false;
                            return;
                        }
                        if (Math.abs(wheelEvent.angleDelta.x) > Math.abs(wheelEvent.angleDelta.y)) {
                            // Horizontal scroll
                            if (wheelEvent.angleDelta.x < 0 && root.currentPage < root.displayPages.length - 1) {
                                root.goToPage(root.currentPage + 1);
                            } else if (wheelEvent.angleDelta.x > 0 && root.currentPage > 0) {
                                root.goToPage(root.currentPage - 1);
                            }
                        } else {
                            // Vertical scroll → map to horizontal paging
                            if (wheelEvent.angleDelta.y < 0 && root.currentPage < root.displayPages.length - 1) {
                                root.goToPage(root.currentPage + 1);
                            } else if (wheelEvent.angleDelta.y > 0 && root.currentPage > 0) {
                                root.goToPage(root.currentPage - 1);
                            }
                        }
                        wheelEvent.accepted = true;
                    }
                }

                NumberAnimation {
                    id: snapAnimation
                    target: flickable
                    property: "contentX"
                    duration: 350
                    easing.type: Easing.OutQuint
                }

                Row {
                    id: pagesRow
                    height: parent.height

                    Repeater {
                        id: pagesRepeater
                        model: root.displayPages.length

                        Item {
                            id: pageContainer
                            required property int index
                            width: flickable.width
                            height: flickable.height

                            // Show only current page content as visible when current
                            property bool isCurrent: root.currentPage === index
                            property list<var> pageToggles: root.positionedPages[index] || []

                            Item {
                                id: pageContentCanvas
                                anchors.horizontalCenter: parent.horizontalCenter
                                width: root.gridWidth
                                anchors.top: parent.top
                                implicitHeight: root.pageHeight(pageContainer.index)
                                height: implicitHeight
                                objectName: "pageContent_" + pageContainer.index

                                StableQuickToggleModel {
                                    id: pageToggleModel
                                    sourceValues: pageContainer.pageToggles
                                }

                                Repeater {
                                    id: gridRepeater
                                    model: pageToggleModel
                                    delegate: AndroidToggleDelegateChooser {

                                        editMode: root.editMode
                                        baseCellWidth: root.baseCellWidth
                                        baseCellHeight: root.baseCellHeight
                                        spacing: root.spacing
                                        isUnused: false
                                        pageIndex: pageContainer.index
                                        gridColumns: root.columns
                                        panel: root
                                        gridRef: pageContentCanvas
                                        entranceTrigger: root.entranceTrigger

                                        onOpenAudioOutputDialog: root.openAudioOutputDialog()
                                        onOpenAudioInputDialog: root.openAudioInputDialog()
                                        onOpenBluetoothDialog: root.openBluetoothDialog()
                                        onOpenNightLightDialog: root.openNightLightDialog()
                                        onOpenWifiDialog: root.openWifiDialog()
                                        onOpenDarkModeDialog: root.openDarkModeDialog()
                                        onOpenLocalSendDialog: root.openLocalSendDialog()
                                        onOpenVpnDialog: root.openVpnDialog()
                                        onOpenTailscaleDialog: root.openTailscaleDialog()
                                        onOpenDnsOverTlsDialog: root.openDnsOverTlsDialog()
                                        onOpenIdleInhibitorDialog: root.openIdleInhibitorDialog()
                                        onOpenScreenShaderDialog: root.openScreenShaderDialog()
                                        onOpenModesDialog: root.openModesDialog()
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }

        // Page indicators (dots)
        Row {
            id: pageIndicators
            anchors.horizontalCenter: parent.horizontalCenter
            spacing: 6
            visible: root.displayPages.length > 1

            Repeater {
                model: root.displayPages.length
                delegate: Rectangle {
                    required property int index
                    width: root.currentPage === index ? 16 : 8
                    height: 8
                    radius: height / 2
                    color: root.currentPage === index ? Appearance.colors.colPrimary : Appearance.colors.colOutlineVariant
                    opacity: root.currentPage === index ? 1.0 : 0.5

                    Behavior on width {
                        animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                    }
                    Behavior on color {
                        animation: Appearance.animation.elementMoveFast.colorAnimation.createObject(this)
                    }
                    Behavior on opacity {
                        animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                    }

                    MouseArea {
                        anchors.fill: parent
                        anchors.margins: -4
                        cursorShape: Qt.PointingHandCursor
                        onClicked: root.goToPage(index)
                    }
                }
            }
        }

        // Edit mode: page navigation + add page buttons
        FadeLoader {
            shown: root.editMode
            fade: false
            anchors {
                left: parent.left
                right: parent.right
            }
            sourceComponent: RowLayout {
                spacing: 6

                // Previous page button
                RippleButton {
                    Layout.preferredWidth: root.baseCellHeight
                    Layout.preferredHeight: root.baseCellHeight * 0.6
                    visible: root.currentPage > 0
                    buttonRadius: Appearance.rounding.full
                    buttonRadiusPressed: height / 2
                    colBackground: Appearance.colors.colSurfaceContainerHigh
                    colBackgroundHover: Appearance.colors.colSurfaceContainerHighest
                    onClicked: root.goToPage(root.currentPage - 1)
                    contentItem: MaterialSymbol {
                        text: "chevron_left"
                        iconSize: Appearance.font.pixelSize.large
                        color: Appearance.colors.colOnSurface
                        horizontalAlignment: Text.AlignHCenter
                    }
                }

                // Page label
                Rectangle {
                    Layout.fillWidth: true
                    Layout.preferredHeight: root.baseCellHeight * 0.6
                    radius: Appearance.rounding.full
                    color: "transparent"
                    border.color: Appearance.colors.colOutline
                    border.width: 1

                    RowLayout {
                        anchors.centerIn: parent
                        spacing: 8
                        MaterialSymbol {
                            text: "auto_awesome_motion"
                            font.pixelSize: Appearance.font.pixelSize.normal
                            color: Appearance.colors.colPrimary
                        }
                        StyledText {
                            text: Translation.tr("Page %1 / %2").arg(root.currentPage + 1).arg(root.displayPages.length)
                            font.pixelSize: Appearance.font.pixelSize.small
                            font.weight: Font.Bold
                            color: Appearance.colors.colOnSurface
                        }
                    }
                }

                // Next page button
                RippleButton {
                    Layout.preferredWidth: root.baseCellHeight
                    Layout.preferredHeight: root.baseCellHeight * 0.6
                    visible: root.currentPage < root.displayPages.length - 1
                    bottomLeftRadius: Appearance.rounding.full
                    topLeftRadius: Appearance.rounding.full
                    bottomRightRadius: Appearance.rounding.verysmall
                    topRightRadius: Appearance.rounding.verysmall
                    buttonRadiusPressed: height / 2
                    colBackground: Appearance.colors.colSurfaceContainerHigh
                    colBackgroundHover: Appearance.colors.colSurfaceContainerHighest
                    onClicked: root.goToPage(root.currentPage + 1)
                    contentItem: MaterialSymbol {
                        text: "chevron_right"
                        iconSize: Appearance.font.pixelSize.large
                        color: Appearance.colors.colOnSurface
                        horizontalAlignment: Text.AlignHCenter
                    }
                }

                // Add page button
                RippleButton {
                    Layout.preferredWidth: root.baseCellHeight
                    Layout.preferredHeight: root.baseCellHeight * 0.6
                    bottomLeftRadius: Appearance.rounding.verysmall
                    topLeftRadius: Appearance.rounding.verysmall
                    bottomRightRadius: Appearance.rounding.verysmall
                    topRightRadius: Appearance.rounding.verysmall
                    buttonRadiusPressed: height / 2
                    colBackground: Appearance.colors.colPrimary
                    colBackgroundHover: Appearance.colors.colPrimaryHover
                    onClicked: root.addPage()
                    contentItem: MaterialSymbol {
                        text: "add"
                        iconSize: Appearance.font.pixelSize.large
                        color: Appearance.colors.colOnPrimary
                        horizontalAlignment: Text.AlignHCenter
                    }
                    StyledToolTip {
                        text: Translation.tr("Add new page")
                    }
                }

                // Delete current page (only if >1 pages and current is empty)
                RippleButton {
                    Layout.preferredWidth: root.baseCellHeight
                    Layout.preferredHeight: root.baseCellHeight * 0.6
                    visible: root.displayPages.length > 1
                    bottomLeftRadius: Appearance.rounding.verysmall
                    topLeftRadius: Appearance.rounding.verysmall
                    bottomRightRadius: Appearance.rounding.full
                    topRightRadius: Appearance.rounding.full
                    buttonRadiusPressed: height / 2
                    colBackground: Appearance.colors.colErrorContainer
                    colBackgroundHover: Appearance.colors.colErrorContainerHover
                    onClicked: root.removePage(root.currentPage)
                    contentItem: MaterialSymbol {
                        text: "delete"
                        iconSize: Appearance.font.pixelSize.large
                        color: Appearance.colors.colOnErrorContainer
                        horizontalAlignment: Text.AlignHCenter
                    }
                    StyledToolTip {
                        text: Translation.tr("Remove current page")
                    }
                }
            }
        }

        // Separator between used and unused toggles in edit mode
        FadeLoader {
            shown: root.editMode
            fade: false
            anchors {
                left: parent.left
                right: parent.right
                leftMargin: root.baseCellHeight / 2
                rightMargin: root.baseCellHeight / 2
            }
            sourceComponent: Rectangle {
                implicitHeight: 1
                color: Appearance.colors.colOutlineVariant
            }
        }

        // Unused toggles (edit mode)
        FadeLoader {
            id: unusedTogglesLoader
            shown: root.editMode
            fade: false
            anchors {
                left: parent.left
                right: parent.right
            }
            sourceComponent: Item {
                id: trayViewport
                implicitHeight: trayFlickable.implicitHeight

                StyledFlickable {
                    id: trayFlickable
                    anchors.fill: parent
                    readonly property real fullHeight: unusedCanvas.implicitHeight + root.trayBadgeOverhang
                    implicitHeight: root.trayMaxHeight < 0 ? fullHeight
                        : Math.min(fullHeight, root.trayMaxHeight)
                    contentWidth: width
                    contentHeight: fullHeight
                    clip: true
                    interactive: !root.externalVerticalScroll && contentHeight > height

                    Item {
                        id: unusedCanvas
                        y: root.trayBadgeOverhang
                        anchors.horizontalCenter: parent.horizontalCenter
                        width: root.gridWidth
                        implicitHeight: Math.max(0, root.packedUnusedToggles.rowsUsed
                            * (root.baseCellHeight + root.spacing) - root.spacing)
                        height: implicitHeight

                        StableQuickToggleModel {
                            id: unusedToggleModel
                            sourceValues: root.positionedUnusedToggles
                        }

                        Repeater {
                            model: unusedToggleModel
                            delegate: AndroidToggleDelegateChooser {

                                editMode: root.editMode
                                baseCellWidth: root.baseCellWidth
                                baseCellHeight: root.baseCellHeight
                                spacing: root.spacing
                                isUnused: true
                                pageIndex: root.currentPage
                                gridColumns: root.columns
                                panel: root
                                gridRef: unusedCanvas

                                onOpenAudioOutputDialog: root.openAudioOutputDialog()
                                onOpenAudioInputDialog: root.openAudioInputDialog()
                                onOpenBluetoothDialog: root.openBluetoothDialog()
                                onOpenNightLightDialog: root.openNightLightDialog()
                                onOpenWifiDialog: root.openWifiDialog()
                                onOpenDarkModeDialog: root.openDarkModeDialog()
                                onOpenLocalSendDialog: root.openLocalSendDialog()
                                onOpenVpnDialog: root.openVpnDialog()
                                onOpenTailscaleDialog: root.openTailscaleDialog()
                                onOpenDnsOverTlsDialog: root.openDnsOverTlsDialog()
                                onOpenIdleInhibitorDialog: root.openIdleInhibitorDialog()
                                onOpenScreenShaderDialog: root.openScreenShaderDialog()
                                onOpenModesDialog: root.openModesDialog()
                            }
                        }
                    }
                }

                ScrollEdgeFade {
                    target: trayFlickable
                    color: root.color
                }
            }
        }
    }

    // Keep flickable in sync with currentPage
    onCurrentPageChanged: {
        if (!flickable.moving) {
            snapAnimation.stop();
            snapAnimation.to = currentPage * flickable.width;
            snapAnimation.start();
        }
    }

    // Clamp currentPage when pages are removed
    onPagesChanged: {
        if (currentPage >= pages.length) {
            currentPage = Math.max(0, pages.length - 1);
        }
    }

}
