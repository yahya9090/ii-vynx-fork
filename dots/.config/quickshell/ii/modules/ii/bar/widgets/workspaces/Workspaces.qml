import qs
import qs.services
import qs.modules.common
import qs.modules.common.models
import qs.modules.common.widgets
import qs.modules.common.functions
import QtQuick
import QtQuick.Effects
import QtQuick.Layouts
import QtQuick.Controls
import QtQuick.Shapes
import Quickshell
import Quickshell.Wayland
import Quickshell.Hyprland
import Quickshell.Widgets
import Qt5Compat.GraphicalEffects

Item {
    id: root

    property bool vertical: false
    readonly property HyprlandMonitor monitor: Hyprland.monitorFor(root.QsWindow.window?.screen)
    readonly property Toplevel activeWindow: ToplevelManager.activeToplevel

    readonly property var currentHyprlandMonitorData: HyprlandData.monitors.find(mon => mon.name === root.monitor?.name)
    readonly property bool scratchpadOpen: !!(currentHyprlandMonitorData && currentHyprlandMonitorData.specialWorkspace && currentHyprlandMonitorData.specialWorkspace.name !== "")
    property real blur: scratchpadOpen ? 1 : 0

    readonly property bool useWorkspaceMap: Config.options.bar.workspaces.useWorkspaceMap
    readonly property list<int> workspaceMap: Config.options.bar.workspaces.workspaceMap
    readonly property int monitorIndex: {
        if (!monitor || !monitor.name) return 0;
        let idx = HyprlandData.monitors.findIndex(mon => mon.name === monitor.name);
        return idx !== -1 ? idx : 0;
    }
    property int workspaceOffset: useWorkspaceMap ? (workspaceMap.length > monitorIndex ? workspaceMap[monitorIndex] : monitorIndex * (Config.options.bar.workspaces.shown || 10)) : 0

    property var shapesList: ["Circle", "Square", "Slanted", "Arch", "Arrow", "SemiCircle", "Oval", "Pill", "Triangle", "Diamond", "ClamShell", "Pentagon", "Gem", "Sunny", "VerySunny", "Cookie4Sided", "Cookie6Sided", "Cookie7Sided", "Cookie9Sided", "Cookie12Sided", "Ghostish", "Clover4Leaf", "Clover8Leaf", "Burst", "SoftBurst", "Flower", "Puffy", "PuffyDiamond", "PixelCircle", "Bun", "Heart"]
    property string currentRandomShape: "Circle"
    property real randomRotation: 0

    // ── Direction arrow ───────────────────────────────────────────────────
    // The active indicator turns into a triangle aimed the way you just moved,
    // holds for a beat, then settles back into a circle. `ShapeCanvas` morphs
    // between polygons on its own whenever `roundedPolygon` changes, so the
    // whole effect is two assignments to `shapeString` — there is no tween to
    // write here, and writing one would fight the built-in morph.
    readonly property bool directionArrowEnabled: Config.options.bar.workspaces.useDirectionArrowForActiveIndicator
    // Both modes need a square indicator: a triangle (or a cookie) stretched
    // across a multi-workspace pill would be sheared rather than rotated.
    readonly property bool squareActiveIndicator: root.directionArrowEnabled
        || Config.options.bar.workspaces.useRandomShapeForActiveIndicator

    // `ShapeCanvas`' own morph runs on the M3 expressive fast-spatial bezier,
    // whose third control point is 1.67 — it overshoots by construction. That
    // is right for a shape *arriving* somewhere, and wrong for a polygon morph:
    // `progress` passes 1 and the interpolation extrapolates past the triangle,
    // so the arrow over-sharpens and settles back. Reads as a flick rather than
    // a turn. The arrow gets a curve with no overshoot instead, and long enough
    // that the triangle is a shape you see rather than a frame you catch.
    readonly property int arrowMorphDuration: Math.round(420 * Appearance.animMultiplier)
    // Time the triangle stands still after the morph finishes. Without it the
    // return starts the instant the arrow is fully formed and the whole gesture
    // reads as a wobble.
    readonly property int arrowRestDuration: Math.round(260 * Appearance.animMultiplier)

    property int previousActiveWorkspaceId: -1
    property bool arrowShown: false
    // Material's Triangle points up, so up is 0 and the rest follow clockwise.
    // On a vertical bar the workspaces run top to bottom, so the same "moved
    // forward" gesture points down instead of right.
    property real arrowRotation: 0

    Timer {
        id: arrowHoldTimer
        // The morph in, and then the rest. Measured from the switch, so the
        // triangle is fully formed for its whole rest.
        interval: root.arrowMorphDuration + root.arrowRestDuration
        repeat: false
        onTriggered: root.arrowShown = false
    }

    function showDirectionArrow(previousId, nextId) {
        if (!root.directionArrowEnabled || previousId < 0 || previousId === nextId)
            return;
        const forward = nextId > previousId;
        if (root.vertical)
            root.arrowRotation = forward ? 180 : 0;
        else
            root.arrowRotation = forward ? 90 : 270;
        root.arrowShown = true;
        arrowHoldTimer.restart();
    }


    function updateRandomShape() {
        if (!Config.options.bar.workspaces.useRandomShapeForActiveIndicator)
            return;
        let nextShape = currentRandomShape;
        let attempts = 0;
        while (nextShape === currentRandomShape && attempts < 10) {
            let randIdx = Math.floor(Math.random() * shapesList.length);
            nextShape = shapesList[randIdx];
            attempts++;
        }
        currentRandomShape = nextShape;
        randomRotation = randomRotation + 90;
    }

    onEffectiveActiveWorkspaceIdChanged: {
        updateRandomShape();
        // The change handler is the only place that still knows both ends of
        // the move: `effectiveActiveWorkspaceId` is a binding and carries no
        // history of its own.
        root.showDirectionArrow(root.previousActiveWorkspaceId, root.effectiveActiveWorkspaceId);
        root.previousActiveWorkspaceId = root.effectiveActiveWorkspaceId;
    }


    readonly property int workspacesShown: {
        if (useWorkspaceMap && workspaceMap.length > monitorIndex) {
            let start = workspaceMap[monitorIndex];
            let end = (monitorIndex + 1 < workspaceMap.length) ? workspaceMap[monitorIndex + 1] : (start + Config.options.bar.workspaces.shown);
            return dynamicWorkspaces ? (end - start) : Config.options.bar.workspaces.shown;
        }
        return Config.options.bar.workspaces.shown;
    }
    readonly property int workspaceGroup: {
        let activeId = monitor?.activeWorkspace?.id;
        if (!activeId)
            return 0;
        if (activeId <= workspaceOffset)
            return 0;
        if (useWorkspaceMap && workspaceMap.length > monitorIndex + 1) {
            let nextMonitorStart = workspaceMap[monitorIndex + 1];
            if (activeId > nextMonitorStart)
                return 0;
        }
        let group = Math.floor((activeId - workspaceOffset - 1) / workspacesShown);
        return Math.max(0, group);
    }
    property list<bool> workspaceOccupied: []
    property int workspaceIndexInGroup: {
        let activeId = monitor?.activeWorkspace?.id;
        if (!activeId)
            return -1;
        let startWs = workspaceOffset + workspaceGroup * workspacesShown + 1;
        let endWs = workspaceOffset + (workspaceGroup + 1) * workspacesShown;
        if (activeId >= startWs && activeId <= endWs) {
            return activeId - startWs;
        }
        return -1;
    }
    property var monitorWindows
    readonly property int effectiveActiveWorkspaceId: monitor?.activeWorkspace?.id ?? (workspaceOffset + 1)

    // Every measurement in this widget derives from these two, so scaling them scales the
    // pills, the active indicator and the window dots together. They were drawn for the
    // 40px bar and stayed that size on a taller one, which is what left the workspace row
    // looking like a strip of desktop pills floating in a touch-sized bar.
    readonly property real contentScale: root.vertical
        ? Appearance.sizes.verticalBarContentScale
        : Appearance.sizes.barContentScale

    property int individualIconBoxHeight: Math.round(22 * root.contentScale)
    property int iconBoxWrapperSize: Math.round(26 * root.contentScale)
    property int workspaceDotSize: 4
    property real iconRatio: 0.8
    property bool showIcons: Config.options.bar.workspaces.showAppIcons

    readonly property bool isScrollingLayout: Persistent.ready && Persistent.states.hyprland && Persistent.states.hyprland.layout === "scrolling"
    property int maxWindowCount: isScrollingLayout ? Config.options.bar.workspaces.maxWindowCount : 1

    readonly property bool dynamicWorkspaces: Config.options.bar.workspaces.dynamicWorkspaces

    function isWorkspaceVisible(wsIndex) {
        const wsId = workspaceGroup * workspacesShown + wsIndex + 1 + workspaceOffset;
        const isActive = wsId === effectiveActiveWorkspaceId;
        const isOccupied = workspaceOccupied[wsIndex];
        return !dynamicWorkspaces || isActive || (isOccupied || false);
    }

    readonly property int visibleActiveIndex: {
        if (!dynamicWorkspaces)
            return workspaceIndexInGroup;
        let count = 0;
        for (let i = 0; i < workspacesShown; i++) {
            if (i === workspaceIndexInGroup)
                return count;
            if (isWorkspaceVisible(i))
                count++;
        }
        return count;
    }

    property bool showNumbersByMs: false
    readonly property bool numbersByInteractionVisible: showNumbersByMs && !GlobalStates.screenLocked && !GlobalStates.workspaceRestoreInProgress
    Timer {
        id: showNumbersTimer
        interval: (Config.options.bar.workspaces.showNumberDelay ?? 100)
        repeat: false
        onTriggered: {
            root.showNumbersByMs = true;
        }
    }
    Connections {
        target: GlobalStates
        function onSuperDownChanged() {
            if (!Config?.options.bar.autoHide.showWhenPressingSuper.enable)
                return;
            if (GlobalStates.superDown)
                showNumbersTimer.restart();
            else {
                showNumbersTimer.stop();
                root.showNumbersByMs = false;
            }
        }
        function onSuperReleaseMightTriggerChanged() {
            showNumbersTimer.stop();
        }
    }

    function updateWorkspaceOccupied() {
        workspaceOccupied = Array.from({
            length: root.workspacesShown
        }, (_, i) => {
            const wsId = workspaceGroup * root.workspacesShown + i + 1 + root.workspaceOffset;
            return Hyprland.workspaces.values.some(ws => ws.id === wsId);
        });
    }

    function hasWindowsInWorkspace(workspaceId) {
        return HyprlandData.windowList.some(w => w.workspace.id === workspaceId);
    }

    function getWindowCountForWorkspace(workspaceId) {
        return HyprlandData.windowList.filter(w => w.workspace.id === workspaceId && !w.floating).length;
    }

    function updateMonitorWindows() {
        const windowsOnMonitor = HyprlandData.windowList.filter(win => win.monitor === root.monitorIndex && !win.floating);
        windowsOnMonitor.sort((a, b) => a.at[0] - b.at[0]);
        root.monitorWindows = windowsOnMonitor.map(win => ({
                    icon: Quickshell.iconPath(AppSearch.guessIcon(win?.class), "image-missing"),
                    workspace: win.workspace?.id
                }));
    }

    // Window list updates
    Connections {
        target: HyprlandData
        function onWindowListChanged() {
            root.updateMonitorWindows();
        }
    }

    Connections {
        target: TaskbarApps
        function onIconThemeRevisionChanged() {
            root.updateMonitorWindows();
        }
    }

    // Occupied workspace updates
    Component.onCompleted: {
        updateWorkspaceOccupied();
        // Seeded, not left at -1: the first switch after startup should draw an
        // arrow like any other, and `showDirectionArrow` refuses a negative
        // previous id precisely so a cold start does not.
        root.previousActiveWorkspaceId = root.effectiveActiveWorkspaceId;
    }
    Connections {
        target: Hyprland.workspaces
        function onValuesChanged() {
            updateWorkspaceOccupied();
        }
    }
    Connections {
        target: Hyprland
        function onFocusedWorkspaceChanged() {
            updateWorkspaceOccupied();
        }
    }
    onWorkspaceGroupChanged: {
        updateWorkspaceOccupied();
    }

    implicitWidth: root.vertical ? Appearance.sizes.verticalBarWidth : contentLayout.implicitWidth
    implicitHeight: root.vertical ? contentLayout.implicitHeight : Appearance.sizes.baseBarHeight

    Behavior on implicitHeight {
        animation: Appearance.animation.barResize.numberAnimation.createObject(this)
    }

    Behavior on blur {
        NumberAnimation {
            duration: Appearance.animation.elementMoveFast.duration
            easing.type: Appearance.animation.elementMoveFast.type
            easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
        }
    }

    Item {
        id: contentContainer
        anchors.fill: parent
        z: 0
        opacity: root.scratchpadOpen ? 0.65 : 1
        layer.enabled: root.blur > 0
        layer.effect: MultiEffect {
            blurEnabled: true
            blurMax: 32
            blur: root.blur
        }

    // Active workspace indicator
    Loader {
        id: activeIndicator
        z: 2
        anchors.horizontalCenter: root.vertical ? parent.horizontalCenter : undefined
        anchors.verticalCenter: root.vertical ? undefined : parent.verticalCenter

        function offsetFor(index) {
            let y = 0;
            if (contentLayout && contentLayout.children) {
                let limit = Math.min(index, contentLayout.children.length);
                for (let i = 0; i < limit; i++) {
                    const item = contentLayout.children[i];
                    if (item) {
                        y += root.vertical ? item.height - baseHeight : item.width - baseHeight;
                    }
                }
            }
            return y;
        }

        function getWindowCount(workspaceId) {
            return HyprlandData.windowList.filter(w => w.workspace.id === workspaceId && !w.floating).length;
        }

        property int index: root.workspaceIndexInGroup
        property int baseHeight: root.iconBoxWrapperSize
        property int windowCount: index < 0 ? 0 : getWindowCount(index + root.workspaceOffset + root.workspaceGroup * root.workspacesShown + 1)

        property bool isEmptyWorkspace: windowCount === 0
        property bool isOneWindow: windowCount === 1

        property real indicatorInsetEmpty: root.iconBoxWrapperSize * 0.07
        property real indicatorInsetOneWindow: root.iconBoxWrapperSize * 0.14
        property real indicatorInset: root.iconBoxWrapperSize * 0.1

        property real visualInset: {
            if (!root.showIcons)
                return indicatorInsetEmpty - 0.5;
            if (isEmptyWorkspace)
                return indicatorInsetEmpty;
            if (isOneWindow)
                return indicatorInsetOneWindow;
            return indicatorInset;
        }

        AnimatedTabIndexPair {
            id: idxPair
            index: Math.max(0, root.visibleActiveIndex)
            easingType: Easing.OutBack
            easingOvershoot: 1.7
            idx1Duration: 250
            idx2Duration: 350
        }

        property real pairMin: Math.min(idxPair.idx1, idxPair.idx2)
        property real pairAbs: Math.abs(idxPair.idx1 - idxPair.idx2)

        property real currentItemOffset: {
            if (root.workspaceIndexInGroup < 0 || !contentLayout || !contentLayout.children || root.workspaceIndexInGroup >= contentLayout.children.length)
                return 0;
            const item = contentLayout.children[root.workspaceIndexInGroup];
            const itemSize = root.vertical ? item?.height : item?.width;
            return (itemSize ?? root.iconBoxWrapperSize) - baseHeight;
        }

        readonly property real accumulatedPreviousOffsets: offsetFor(root.workspaceIndexInGroup + 1)

        readonly property real baseIndicatorPosition: pairMin * root.iconBoxWrapperSize
        readonly property real baseIndicatorLength: (pairAbs + 1) * root.iconBoxWrapperSize

        readonly property real contentLayoutOffset: contentLayout ? (root.vertical ? (root.height - contentLayout.implicitHeight) / 2 : (root.width - contentLayout.implicitWidth) / 2) : 0

        property real indicatorPosition: baseIndicatorPosition + accumulatedPreviousOffsets - currentItemOffset + visualInset
        property real indicatorLength: baseIndicatorLength + currentItemOffset - visualInset * 2

        y: root.vertical ? (root.squareActiveIndicator ? (indicatorPosition + (indicatorLength - individualIconBoxHeight) / 2) : indicatorPosition) : 0
        x: root.vertical ? 0 : (root.squareActiveIndicator ? (indicatorPosition + (indicatorLength - individualIconBoxHeight) / 2) : indicatorPosition)
        width: root.vertical ? individualIconBoxHeight : (root.squareActiveIndicator ? individualIconBoxHeight : indicatorLength)
        height: root.vertical ? (root.squareActiveIndicator ? individualIconBoxHeight : indicatorLength) : individualIconBoxHeight

        opacity: root.scratchpadOpen || root.workspaceIndexInGroup < 0 ? 0.0 : 1.0
        Behavior on opacity {
            NumberAnimation {
                duration: Appearance.animation.elementMoveFast.duration
                easing.type: Appearance.animation.elementMoveFast.type
                easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
            }
        }

        sourceComponent: (Config.options.bar.workspaces.useMaterialShapeForActiveIndicator || root.squareActiveIndicator) ? materialShapeComponent : rectangleComponent

        Component {
            id: rectangleComponent
            Rectangle {
                radius: Appearance.rounding.full
                color: Appearance.colors.colPrimary
                opacity: Config.options.bar.workspaces.activeIndicatorOpacity / 100
            }
        }

        Component {
            id: materialShapeComponent
            MaterialShape {
                anchors.fill: parent
                transformOrigin: Item.Center
                shapeString: {
                    // The arrow outranks the random shape: a triangle that only
                    // sometimes points the right way is worse than no arrow.
                    if (root.directionArrowEnabled)
                        return root.arrowShown ? "Triangle" : "Circle";
                    if (Config.options.bar.workspaces.useRandomShapeForActiveIndicator)
                        return root.currentRandomShape;
                    return Config.options.bar.workspaces.activeIndicatorShape;
                }
                color: Appearance.colors.colPrimary
                opacity: Config.options.bar.workspaces.activeIndicatorOpacity / 100

                // Replaces the one ShapeCanvas ships with. Only the arrow wants
                // a settling curve; the random shape is a shape *landing*, and
                // the overshoot is what gives that its snap.
                animation: NumberAnimation {
                    duration: root.directionArrowEnabled
                        ? root.arrowMorphDuration
                        : Math.round(350 * Appearance.animMultiplier)
                    easing.type: root.directionArrowEnabled ? Easing.InOutCubic : Easing.BezierSpline
                    easing.bezierCurve: [0.42, 1.67, 0.21, 0.90, 1, 1]
                }

                rotation: {
                    if (root.directionArrowEnabled)
                        return root.arrowRotation;
                    return Config.options.bar.workspaces.useRandomShapeForActiveIndicator ? root.randomRotation : 0;
                }
                Behavior on rotation {
                    // Off in arrow mode on purpose. The triangle has to be
                    // aimed before it appears; animating the angle would spin
                    // it into place — and `Clockwise` would take the long way
                    // round on a backwards switch, pointing every wrong
                    // direction on the way there.
                    enabled: !root.directionArrowEnabled
                    RotationAnimation {
                        duration: 350
                        direction: RotationAnimation.Clockwise
                        easing.type: Easing.OutBack
                    }
                }
            }
        }
    }

    Rectangle { // NOTE: we still dont have an unhover animation
        id: hoverIndicator
        z: 2
        anchors.horizontalCenter: root.vertical ? parent.horizontalCenter : undefined
        anchors.verticalCenter: root.vertical ? undefined : parent.verticalCenter

        color: "transparent"
        radius: Appearance.rounding.full

        visible: interactionMouseArea.containsMouse
        opacity: visible ? 1 : 0

        property int hoverIdx: interactionMouseArea.hoverIndex
        property bool wasVisible: false

        onVisibleChanged: { // we disable the animations on first contact, then enable it
            if (visible && !wasVisible) {
                positionBehavior.enabled = false;
                lengthBehavior.enabled = false;

                Qt.callLater(function () {
                    positionBehavior.enabled = true;
                    lengthBehavior.enabled = true;
                });
            }
            wasVisible = visible;
        }

        function offsetFor(index) {
            let y = 0;
            for (let i = 0; i < index; i++) {
                const item = contentLayout.children[i];
                y += root.vertical ? item?.height - root.iconBoxWrapperSize : item?.width - root.iconBoxWrapperSize;
            }
            return y;
        }

        property real currentItemOffset: {
            const item = contentLayout.children[hoverIdx];
            const itemSize = root.vertical ? item?.height : item?.width;
            return itemSize - root.iconBoxWrapperSize;
        }

        readonly property real accumulatedPreviousOffsets: offsetFor(hoverIdx)

        property real indicatorPosition: hoverIdx * root.iconBoxWrapperSize + accumulatedPreviousOffsets + root.iconBoxWrapperSize * 0.05
        property real indicatorLength: root.iconBoxWrapperSize + currentItemOffset - root.iconBoxWrapperSize * 0.1

        y: root.vertical ? indicatorPosition : 0
        x: root.vertical ? 0 : indicatorPosition
        implicitHeight: root.vertical ? indicatorLength : individualIconBoxHeight
        implicitWidth: root.vertical ? individualIconBoxHeight : indicatorLength

        Behavior on indicatorPosition {
            id: positionBehavior
            animation: Appearance.animation.elementMove.numberAnimation.createObject(hoverIndicator)
        }
        Behavior on indicatorLength {
            id: lengthBehavior
            animation: Appearance.animation.elementMove.numberAnimation.createObject(hoverIndicator)
        }

        Behavior on opacity {
            animation: Appearance.animation.elementMove.numberAnimation.createObject(hoverIndicator)
        }

        HoverOverlay {
            hover: interactionMouseArea.containsMouse
        }
    }

    MouseArea {
        id: interactionMouseArea
        z: 4
        anchors.fill: parent
        cursorShape: Qt.PointingHandCursor
        hoverEnabled: true
        acceptedButtons: Qt.RightButton | Qt.LeftButton | Qt.BackButton

        property int hoverIndex: {
            const position = root.vertical ? mouseY : mouseX;
            let accumulated = 0;

            // calculating the every workspace's length
            for (let i = 0; i < root.workspacesShown; i++) {
                const item = contentLayout.children[i];
                if (!item)
                    continue;

                const itemSize = root.vertical ? item.height : item.width;

                if (position < accumulated + itemSize) {
                    return i;
                }

                accumulated += itemSize;
            }

            return root.workspacesShown - 1;
        }

        onPressed: event => {
            if (event.button === Qt.RightButton) {
                if (PanelFamily.current === "akebono")
                    GlobalStates.desktopOverviewOpen = !GlobalStates.desktopOverviewOpen;
                else
                    GlobalStates.overviewOpen = !GlobalStates.overviewOpen;
            }
            if (event.button === Qt.BackButton) {
                Hyprland.dispatch(`hl.dsp.workspace.toggle_special("special")`);
            }
            if (event.button === Qt.LeftButton) {
                const wsId = workspaceOffset + workspaceGroup * workspacesShown + hoverIndex + 1;
                Hyprland.dispatch(`hl.dsp.focus({ workspace = "${wsId}" })`);
            }
        }

        onWheel: event => {
            event.accepted = true;
            if (dynamicWorkspaces) {
                if (event.angleDelta.y < 0)
                    Hyprland.dispatch("hl.dsp.focus({workspace = 'r+1'})");
                else if (event.angleDelta.y > 0)
                    Hyprland.dispatch("hl.dsp.focus({workspace = 'r-1'})");
            } else {
                let nextId = effectiveActiveWorkspaceId + (event.angleDelta.y < 0 ? 1 : -1);
                if (nextId < 1)
                    return;
                if (useWorkspaceMap) {
                    const nextMonitorStart = workspaceMap[monitorIndex + 1] ?? (workspaceMap[monitorIndex] + workspacesShown);
                    if (nextId < workspaceOffset + 1 || nextId > nextMonitorStart)
                        return;
                }
                Hyprland.dispatch("hl.dsp.focus({ workspace = '" + nextId + "' })");
            }
        }
    }

    StyledRectangle {
        id: occupiedIndicatorsBg
        anchors.fill: occupiedIndicatorsLayout
        contentLayer: StyledRectangle.ContentLayer.Group
        color: ColorUtils.transparentize(Appearance.m3colors.m3secondaryContainer, 0.4)
        visible: false
    }

    GridLayout {
        id: occupiedIndicatorsLayout
        anchors.centerIn: parent
        columnSpacing: 0
        rowSpacing: 0
        z: 1

        columns: root.vertical ? 1 : 99
        rows: root.vertical ? 99 : 1

        layer.enabled: true
        visible: false

        Repeater {
            model: root.workspacesShown
            delegate: Item {
                id: wsBg
                Layout.alignment: Qt.AlignCenter

                property int wsId: workspaceGroup * workspacesShown + index + 1 + workspaceOffset
                property bool currentOccupied: (workspaceOccupied[index] || false) && wsId != effectiveActiveWorkspaceId
                property bool previousOccupied: index > 0 && (workspaceOccupied[index - 1] || false) && (wsId - 1) != effectiveActiveWorkspaceId
                property bool nextOccupied: index < workspacesShown - 1 && (workspaceOccupied[index + 1] || false) && (wsId + 1) != effectiveActiveWorkspaceId

                property int windowCount: root.getWindowCountForWorkspace(wsId)

                property real itemSize: {
                    const item = contentLayout.children[index];
                    return root.vertical ? (item?.height ?? root.iconBoxWrapperSize) : (item?.width ?? root.iconBoxWrapperSize);
                }

                implicitWidth: root.vertical ? root.iconBoxWrapperSize : (wsBg.wsVisible ? itemSize : 0)
                implicitHeight: root.vertical ? (wsBg.wsVisible ? itemSize : 0) : root.iconBoxWrapperSize
                property bool wsVisible: root.isWorkspaceVisible(index)

                Pill {
                    property real stretchAmount: 12 // not using multiplier because it mulitplies multi-windowed workspaces A LOT

                    property real undirectionalWidth: root.iconBoxWrapperSize * wsBg.currentOccupied

                    property real undirectionalLength: {
                        if (!wsBg.currentOccupied)
                            return 0;

                        let baseLength = wsBg.itemSize;

                        if (wsBg.previousOccupied && index > 0) {
                            baseLength += stretchAmount;
                        }

                        if (wsBg.nextOccupied && index < workspacesShown - 1) {
                            baseLength += stretchAmount;
                        }

                        return baseLength;
                    }

                    property real undirectionalOffset: {
                        if (!wsBg.currentOccupied)
                            return 0.5 * root.iconBoxWrapperSize;

                        if (!wsBg.previousOccupied || index === 0)
                            return 0;

                        return -stretchAmount;
                    }

                    anchors.verticalCenter: root.vertical ? undefined : parent.verticalCenter
                    anchors.horizontalCenter: root.vertical ? parent.horizontalCenter : undefined
                    x: root.vertical ? 0 : undirectionalOffset
                    y: root.vertical ? undirectionalOffset : 0
                    implicitWidth: root.vertical ? undirectionalWidth : undirectionalLength
                    implicitHeight: root.vertical ? undirectionalLength : undirectionalWidth

                    Behavior on undirectionalWidth {
                        animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
                    }
                    Behavior on undirectionalLength {
                        animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
                    }
                    Behavior on undirectionalOffset {
                        animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
                    }
                }
            }
        }
    }

    MultiEffect {
        id: occupiedIndicatorsMultiEffect
        z: 1
        anchors.centerIn: parent
        implicitWidth: occupiedIndicatorsLayout.implicitWidth
        implicitHeight: occupiedIndicatorsLayout.implicitHeight
        source: occupiedIndicatorsBg
        maskEnabled: true
        maskSource: occupiedIndicatorsLayout
        maskThresholdMin: 0.5
        maskSpreadAtMin: 1.0
    }

    GridLayout {
        id: contentLayout
        anchors.centerIn: parent
        columnSpacing: 0
        rowSpacing: 0
        z: 3

        columns: root.vertical ? 1 : 99
        rows: root.vertical ? 99 : 1

        Repeater {
            id: workspaceRepeater
            model: root.workspacesShown

            delegate: Item {
                id: background
                Layout.alignment: Qt.AlignCenter

                visible: wsVisible
                property bool wsVisible: root.isWorkspaceVisible(index)
                implicitWidth: root.vertical ? root.iconBoxWrapperSize : (Math.max(layout.implicitWidth + 8, root.iconBoxWrapperSize))
                implicitHeight: root.vertical ? (Math.max(layout.implicitHeight + 8, root.iconBoxWrapperSize)) : root.iconBoxWrapperSize

                readonly property bool isShowingScratchpad: root.scratchpadOpen && (index === root.workspaceIndexInGroup)

                Behavior on implicitWidth {
                    animation: Appearance.animation.barResize.numberAnimation.createObject(this)
                }
                Behavior on implicitHeight {
                    animation: Appearance.animation.barResize.numberAnimation.createObject(this)
                }

                Item {
                    id: normalContentWrapper
                    anchors.fill: parent

                    opacity: background.isShowingScratchpad ? 0.0 : 1.0
                    scale: background.isShowingScratchpad ? 0.8 : 1.0

                    Behavior on opacity {
                        NumberAnimation {
                            duration: Appearance.animation.elementMoveFast.duration
                            easing.type: Appearance.animation.elementMoveFast.type
                            easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
                        }
                    }
                    Behavior on scale {
                        NumberAnimation {
                            duration: Appearance.animation.elementMoveFast.duration
                            easing.type: Appearance.animation.elementMoveFast.type
                            easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
                        }
                    }

                    WorkspaceBackgroundIndicator {
                        workspaceValue: workspaceOffset + workspaceGroup * workspacesShown + index + 1
                        activeWorkspace: monitor?.activeWorkspace?.id === workspaceValue
                    }

                    GridLayout {
                        id: layout
                        anchors.centerIn: parent
                        columnSpacing: 0
                        rowSpacing: 0
                        columns: root.vertical ? 1 : 99
                        rows: root.vertical ? 99 : 1

                        Repeater {
                            property int workspaceIndex: workspaceOffset + workspaceGroup * workspacesShown + index + 1
                            model: root.showIcons ? root.monitorWindows?.filter(win => win.workspace === workspaceIndex).splice(0, Config.options.bar.workspaces.maxWindowCount) : []
                            delegate: Item {
                                id: iconContainer
                                Layout.alignment: Qt.AlignHCenter
                                width: root.individualIconBoxHeight
                                height: root.individualIconBoxHeight

                                layer.enabled: Config.options.appearance.icons.enableShapeMask
                                layer.effect: OpacityMask {
                                    maskSource: iconMask
                                }

                                MaterialShape {
                                    id: iconMask
                                    anchors.fill: parent
                                    shapeString: Config.options.appearance.icons.shapeMask
                                    visible: false
                                }

                                IconImage {
                                    id: mainAppIcon
                                    Layout.alignment: Qt.AlignHCenter
                                    anchors {
                                        left: parent.left
                                        top: parent.top
                                        leftMargin: root.numbersByInteractionVisible ? 15 : 2
                                        topMargin: root.numbersByInteractionVisible ? 15 : 2
                                    }
                                    source: modelData.icon
                                    implicitSize: (root.individualIconBoxHeight * root.iconRatio) * (root.numbersByInteractionVisible ? 1 / 1.5 : 1)

                                    // Force reload when the icon theme regenerates; decode at the stable
                                    // base size so hover animations don't re-decode every frame. Resolved
                                    // on this thread: the shared icon loader is not safe to read from Qt's
                                    // image thread while the theme is changing under it.
                                    asynchronous: false
                                    backer.cache: false
                                    backer.sourceSize: Qt.size(
                                        root.individualIconBoxHeight * root.iconRatio + TaskbarApps.iconThemeRevision,
                                        root.individualIconBoxHeight * root.iconRatio + TaskbarApps.iconThemeRevision)

                                    Behavior on anchors.leftMargin {
                                        animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                                    }
                                    Behavior on anchors.topMargin {
                                        animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
                                    }
                                    Behavior on implicitSize {
                                        animation: Appearance.animation.barResize.numberAnimation.createObject(this)
                                    }

                                    layer.enabled: Config.options.appearance.icons.enableShapeMask
                                    layer.effect: OpacityMask {
                                        maskSource: iconMask
                                    }
                                }
                                Loader {
                                    active: Config.options.bar.workspaces.monochromeIcons
                                    anchors.fill: mainAppIcon
                                    sourceComponent: Item {
                                        Desaturate {
                                            id: desaturatedIcon
                                            visible: false
                                            anchors.fill: parent
                                            source: mainAppIcon
                                            desaturation: 0.8
                                            layer.enabled: Config.options.appearance.icons.enableShapeMask
                                            layer.effect: OpacityMask {
                                                maskSource: iconMask
                                            }
                                        }
                                        ColorOverlay {
                                            anchors.fill: desaturatedIcon
                                            source: desaturatedIcon
                                            color: ColorUtils.transparentize(Appearance.colors.colPrimary, Config.options.appearance.iconTintPercentage)
                                        }
                                    }
                                }
                            }
                        }
                    }
                }

            }
        }
    }

        Item {
            id: scratchpadPositionHelper
            readonly property real indicatorSize: root.individualIconBoxHeight + 2
            readonly property Item activeItem: (contentLayout.children && root.workspaceIndexInGroup >= 0 && root.workspaceIndexInGroup < contentLayout.children.length) ? contentLayout.children[root.workspaceIndexInGroup] : null

            x: activeItem ? root.vertical ? contentLayout.x + (contentLayout.width - indicatorSize) / 2 : activeItem.x + contentLayout.x + (activeItem.width - indicatorSize) / 2 : 0
            y: activeItem ? root.vertical ? activeItem.y + contentLayout.y + (activeItem.height - indicatorSize) / 2 : contentLayout.y + (contentLayout.height - indicatorSize) / 2 : 0
            width: indicatorSize
            height: indicatorSize
            visible: false
        }
    }

    Item {
        id: scratchpadOverlay
        z: 10

        x: scratchpadPositionHelper.x
        y: scratchpadPositionHelper.y
        width: scratchpadPositionHelper.width
        height: scratchpadPositionHelper.height

        readonly property bool _show: root.scratchpadOpen && root.workspaceIndexInGroup >= 0

        visible: _show
        opacity: _show ? 1.0 : 0.0
        scale: _show ? 1.0 : 0.7

        Behavior on opacity {
            NumberAnimation {
                duration: Appearance.animation.elementMoveFast.duration
                easing.type: Appearance.animation.elementMoveFast.type
                easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
            }
        }
        Behavior on scale {
            NumberAnimation {
                duration: Appearance.animation.elementMoveFast.duration
                easing.type: Appearance.animation.elementMoveFast.type
                easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
            }
        }

        MaterialShape {
            anchors.fill: parent
            shapeString: "Flower"
            color: Appearance.colors.colTertiary
        }

        Rectangle {
            anchors.centerIn: parent
            width: 4
            height: 4
            radius: 2
            color: Appearance.colors.colOnTertiary
            opacity: 1.0

            SequentialAnimation on opacity {
                loops: Animation.Infinite
                running: root.scratchpadOpen
                NumberAnimation {
                    to: 0.3
                    duration: 1500
                    easing.type: Easing.InOutQuad
                }
                NumberAnimation {
                    to: 1.0
                    duration: 1500
                    easing.type: Easing.InOutQuad
                }
            }
        }
    }

    component HoverOverlay: Rectangle {
        id: hoverOverlay
        anchors.fill: parent

        property bool hover: false

        color: Appearance.colors.colPrimary
        radius: Appearance.rounding.full
        opacity: hover ? 0.1 : 0

        Behavior on opacity {
            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
        }
    }

    component WorkspaceBackgroundIndicator: Rectangle {
        id: wsIndicator

        property bool showNumbers: !GlobalStates.screenLocked && !GlobalStates.workspaceRestoreInProgress && (Config.options.bar.workspaces.alwaysShowNumbers || root.numbersByInteractionVisible)
        property int workspaceValue
        property bool activeWorkspace
        // Base indicator shown behind/in place of content: "dot" | "icon"
        property string indicatorStyle: Config.options.bar.workspaces.indicatorStyle ?? "dot"
        property bool useIconIndicator: indicatorStyle === "icon" && !showNumbers
        property color indColor: (activeWorkspace) ? Appearance.m3colors.m3onPrimary : (root.workspaceOccupied[index] ? Appearance.m3colors.m3onSecondaryContainer : Appearance.colors.colOnLayer1Inactive)

        function symbolForWorkspace(wsId) {
            switch (wsId) {
                case 1:  return "code";
                case 2:  return "public";
                case 3:  return "music_note";
                case 4:  return "edit_square";
                case 5:  return "image";
                case 6:  return "forum";
                case 7:  return "browser_updated";
                case 8:  return "finance_mode";
                case 9:  return "monitor";
                case 10: return "analytics";
                default: return "circle";
            }
        }

        anchors.centerIn: parent
        width: useIconIndicator ? root.iconBoxWrapperSize * 0.5 : root.workspaceDotSize
        height: width
        radius: width / 2
        visible: layout.implicitHeight + 8 < root.iconBoxWrapperSize
            || Config.options.bar.workspaces.alwaysShowNumbers
            || root.numbersByInteractionVisible
        color: (!showNumbers && !useIconIndicator) ? indColor : "transparent"

        Behavior on color {
            animation: Appearance.animation.elementMoveFast.colorAnimation.createObject(this)
        }

        Behavior on width {
            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
        }

        StyledText {
            opacity: wsIndicator.showNumbers ? 1 : 0
            anchors.centerIn: parent
            text: Config.options?.bar.workspaces.numberMap[workspaceValue - 1] || workspaceValue
            font.weight: Font.Black
            horizontalAlignment: Text.AlignHCenter
            verticalAlignment: Text.AlignVCenter
            elide: Text.ElideRight
            color: indColor
            Behavior on opacity {
                animation: Appearance.animation.elementMoveSlow.numberAnimation.createObject(this)
            }
        }

        MaterialSymbol {
            visible: wsIndicator.useIconIndicator
            opacity: wsIndicator.useIconIndicator ? 1 : 0
            anchors.centerIn: parent
            text: wsIndicator.symbolForWorkspace(workspaceValue)
            iconSize: wsIndicator.width
            color: indColor

            Behavior on opacity {
                animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(this)
            }
        }
    }
}
