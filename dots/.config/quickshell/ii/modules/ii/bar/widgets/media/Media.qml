import qs.modules.common
import qs.modules.common.widgets
import qs.services
import qs
import qs.modules.common.functions
import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Services.Mpris
import Quickshell.Hyprland
import Qt5Compat.GraphicalEffects
import qs.modules.common.utils

Item {
    id: root
    Layout.fillHeight: true

    readonly property MprisPlayer activePlayer: MprisController.activePlayer
    readonly property string cleanedTitle: StringUtils.cleanMusicTitle(activePlayer?.trackTitle) || Translation.tr("No media")
    readonly property bool hasTrack: (activePlayer?.trackTitle ?? "").length > 0

    visible: hasTrack

    onHasTrackChanged: {
        if (typeof rootItem !== "undefined") {
            rootItem.toggleVisible(hasTrack);
        }
    }

    property int customSize: Config.options.bar.mediaPlayer.customSize
    property bool disablePopup: false

    property int lyricsCustomSize: Config.options.bar.mediaPlayer.lyrics.customSize
    readonly property int maxWidth: 300

    property bool useFixedSize: Config.options.bar.mediaPlayer.useFixedSize
    readonly property bool lyricsEnabled: Config.options.bar.mediaPlayer.lyrics.enable
    readonly property bool useGradientMask: Config.options.bar.mediaPlayer.lyrics.useGradientMask
    readonly property string lyricsStyle: Config.options.bar.mediaPlayer.lyrics.style
    readonly property bool artworkEnabled: Config.options.bar.mediaPlayer.artwork.enable

    readonly property int progressButtonSize: 20
    readonly property int artworkBoxSize: artworkEnabled ? Math.min(25, Appearance.sizes.baseBarHeight - 8) : 0
    readonly property int artworkContentPadding: artworkEnabled ? 6 : 0

    property int textMetricsSpacing: artworkEnabled ? 70 : 50
    property int textMetricsAdvance: Math.min(textMetrics.advanceWidth + textMetricsSpacing, Config.options.bar.mediaPlayer.maxSize)
    implicitWidth: hasTrack ? (LyricsService.hasSyncedLines && root.lyricsEnabled ? lyricsCustomSize : useFixedSize ? customSize : textMetricsAdvance) : 0
    implicitHeight: hasTrack ? Appearance.sizes.baseBarHeight : 0

    Behavior on implicitWidth {
        animation: Appearance.animation.barResize.numberAnimation.createObject(this)
    }

    function updatePopupRect() {
        if (root.visible && root.width > 0 && root.height > 0) {
            var globalPos = root.mapToItem(null, 0, 0);
            GlobalStates.mediaPopupRect = Qt.rect(globalPos.x, globalPos.y, root.width, root.height);
        }
    }

    onVisibleChanged: if (visible) Qt.callLater(updatePopupRect)
    onWidthChanged: if (visible) Qt.callLater(updatePopupRect)
    onXChanged: if (visible) Qt.callLater(updatePopupRect)
    onYChanged: if (visible) Qt.callLater(updatePopupRect)

    Connections {
        target: GlobalStates
        function onMediaControlsOpenChanged() {
            if (GlobalStates.mediaControlsOpen && root.visible) {
                root.updatePopupRect();
            }
        }
    }

    Component.onCompleted: {
        LyricsService.initiliazeLyrics();
        if (typeof rootItem !== "undefined") {
            rootItem.toggleVisible(hasTrack);
        }
        Qt.callLater(updatePopupRect);
    }

    readonly property string artUrl: MprisController.artUrl
    readonly property bool isLocalArt: artUrl.startsWith("file://")
    property string artDownloadLocation: Directories.coverArt
    property string artFileName: Qt.md5(artUrl)
    property string artFilePath: `${artDownloadLocation}/${artFileName}`
    property bool artDownloaded: false

    readonly property string artSource: {
        if (!artUrl)
            return "";
        if (isLocalArt)
            return artUrl;
        return artDownloaded ? Qt.resolvedUrl(artFilePath) : "";
    }

    onArtFilePathChanged: {
        if (!artUrl || artUrl.length === 0) {
            artDownloaded = false;
            return;
        }
        if (isLocalArt) {
            artDownloaded = true;
            return;
        }
        artDownloader.targetFile = artUrl;
        artDownloader.artFilePath = artFilePath;
        artDownloader.artTempPath = artFilePath + ".tmp";
        artDownloaded = false;
        artDownloader.running = true;
    }

    Process {
        id: artDownloader
        property string targetFile: root.artUrl
        property string artFilePath: root.artFilePath
        property string artTempPath: root.artFilePath + ".tmp"
        command: ["bash", "-c", `[ -f ${artFilePath} ] || (curl -4 -sSL '${targetFile}' -o '${artTempPath}' && mv '${artTempPath}' '${artFilePath}')`]
        onExited: {
            artDownloaded = true;
        }
    }

    Item {
        id: artworkItem
        visible: artworkEnabled
        anchors.left: parent.left
        anchors.verticalCenter: parent.verticalCenter
        width: artworkEnabled ? artworkBoxSize : 0
        height: artworkEnabled ? artworkBoxSize : 0

        Rectangle {
            anchors.fill: parent
            color: Appearance.colors.colPrimaryContainer
            radius: Appearance.rounding.full

            Image {
                anchors.fill: parent
                source: root.artSource
                fillMode: Image.PreserveAspectCrop
                cache: false
                antialiasing: true
                width: parent.width
                height: parent.height
                sourceSize.width: width
                sourceSize.height: height

                layer.enabled: true
                layer.effect: OpacityMask {
                    maskSource: Rectangle {
                        width: artworkItem.width
                        height: artworkItem.height
                        radius: Appearance.rounding.full
                    }
                }
            }

            MaterialSymbol {
                anchors.centerIn: parent
                visible: root.artSource.length === 0
                fill: 1
                text: "music_note"
                iconSize: Math.max(12, artworkItem.width * 0.5)
                color: Appearance.colors.colOnSecondaryContainer
            }
        }
    }

    MouseArea {
        id: mediaMouseArea
        anchors.fill: parent
        hoverEnabled: !BarInteraction.clickToShow
        acceptedButtons: Qt.MiddleButton | Qt.BackButton | Qt.ForwardButton | Qt.RightButton | Qt.LeftButton
        cursorShape: Qt.PointingHandCursor
        onEntered: {
            GlobalStates.setMediaWidgetHovered(true);
            if (root.disablePopup)
                return;
            if (hoverEnabled) {
                var globalPos = root.mapToItem(null, 0, 0);
                GlobalStates.mediaPopupRect = Qt.rect(globalPos.x, globalPos.y, root.width, root.height);
                GlobalStates.mediaControlsOpen = true;
            }
        }
        onExited: {
            GlobalStates.setMediaWidgetHovered(false);
        }
        onPressed: event => {
            if (event.button === Qt.MiddleButton) {
                activePlayer.togglePlaying();
            } else if (event.button === Qt.BackButton) {
                activePlayer.previous();
            } else if (event.button === Qt.ForwardButton || event.button === Qt.RightButton) {
                activePlayer.next();
            } else if (event.button === Qt.LeftButton) {
                if (!hoverEnabled && !root.disablePopup) {
                    var globalPos = root.mapToItem(null, 0, 0);
                    GlobalStates.mediaPopupRect = Qt.rect(globalPos.x, globalPos.y, root.width, root.height);
                    GlobalStates.mediaControlsOpen = !GlobalStates.mediaControlsOpen;
                }
            }
        }
        onWheel: event => {
            if (!Config.options.bar.mediaPlayer.enableVolumeScroll)
                return;
            if (event.angleDelta.y > 0)
                MprisController.incrementVolume();
            else if (event.angleDelta.y < 0)
                MprisController.decrementVolume();
            event.accepted = true;
        }
    }

    Item {
        id: mediaCircProgSlot
        width: root.progressButtonSize
        height: root.progressButtonSize
        anchors.verticalCenter: parent.verticalCenter
        x: artworkEnabled ? root.width - width : 0

        ClippedFilledCircularProgress {
            id: mediaCircProg
            anchors.fill: parent
            visible: true
            implicitSize: root.progressButtonSize

            lineWidth: Appearance.rounding.unsharpen
            value: (activePlayer?.length ?? 0) > 0 ? Math.min(1, Math.max(0, activePlayer.position / activePlayer.length)) : 0
            colPrimary: Appearance.colors.colOnSecondaryContainer
            enableAnimation: false

            Item {
                anchors.centerIn: parent
                width: mediaCircProg.implicitSize
                height: mediaCircProg.implicitSize

                MaterialSymbol {
                    anchors.centerIn: parent
                    fill: 1
                    text: activePlayer?.isPlaying ? "pause" : "music_note"
                    iconSize: Appearance.font.pixelSize.normal
                    color: Appearance.m3colors.m3onSecondaryContainer
                }
            }
        }
    }

    TextMetrics {
        id: textMetrics
        text: `${cleanedTitle}${activePlayer?.trackArtist ? ' • ' + activePlayer.trackArtist : ''}`
    }

    StyledText {
        visible: (!LyricsService.hasSyncedLines || !lyricsEnabled)
        anchors {
            horizontalCenter: parent.horizontalCenter
            horizontalCenterOffset: artworkEnabled ? 0 : mediaCircProgSlot.width / 2
            verticalCenter: parent.verticalCenter
            verticalCenterOffset: 1 // to vertically center it
        }
        horizontalAlignment: Text.AlignHCenter
        width: artworkEnabled ? parent.implicitWidth - (artworkItem.width + mediaCircProgSlot.width + artworkContentPadding + 16) : parent.implicitWidth - mediaCircProgSlot.width - 16
        elide: Text.ElideRight
        color: Appearance.colors.colOnLayer1
        text: `${cleanedTitle}${activePlayer?.trackArtist ? ' • ' + activePlayer.trackArtist : ''}`
    }

    Loader {
        id: lyricsItemLoader
        active: lyricsEnabled

        width: artworkEnabled ? parent.width - (artworkItem.width + mediaCircProg.implicitSize * 2) : parent.width - mediaCircProg.implicitSize * 2
        height: parent.height

        anchors.left: parent.left
        anchors.leftMargin: artworkEnabled ? mediaCircProg.implicitSize * 1.5 + artworkContentPadding : mediaCircProg.implicitSize * 1.5

        sourceComponent: Item {
            id: lyricsItem
            visible: lyricsEnabled

            anchors.centerIn: parent

            Loader {
                active: lyricsStyle == "static"
                anchors.fill: parent
                anchors.centerIn: parent
                sourceComponent: LyricsStatic {
                    anchors.fill: parent
                    anchors.centerIn: parent
                    horizontalAlignment: Text.AlignHCenter
                }
            }

            Loader {
                active: lyricsStyle == "scroller"
                anchors.fill: parent
                sourceComponent: LyricScroller {
                    id: lyricScroller

                    anchors.fill: parent
                    visible: lyricsStyle == "scroller" && LyricsService.hasSyncedLines

                    defaultLyricsSize: Appearance.font.pixelSize.smallest
                    useGradientMask: root.useGradientMask
                    halfVisibleLines: 1
                    downScale: 0.98
                    rowHeight: 10
                    gradientDensity: 0.25
                }
            }
        }
    }
}
