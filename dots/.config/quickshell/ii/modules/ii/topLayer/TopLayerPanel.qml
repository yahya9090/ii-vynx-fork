import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Wayland
import Quickshell.Hyprland
import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import qs.modules.ii.bar as Bar
import qs.modules.ii.bar.shared
import qs.modules.ii.verticalBar as VBar
import qs.modules.ii.sidebarPolicies as Policies
import qs.modules.ii.sidebarDashboard as Dashboard
import qs.modules.ii.wrappedFrame as Frame
import qs.modules.ii.topLayer.search as SearchConnect
import qs.modules.ii.topLayer.osd as OsdConnect
import qs.modules.ii.overview

PanelWindow {
    id: topPanel
    color: "transparent"
    WlrLayershell.namespace: "quickshell:topLayer"
    WlrLayershell.layer: searchOpenOnMonitor ? WlrLayer.Overlay : WlrLayer.Top
    exclusionMode: ExclusionMode.Ignore

    anchors {
        top: true
        bottom: true
        left: true
        right: true
    }

    readonly property bool usingWrappedFrame: Config.options.appearance.fakeScreenRounding === 3 && hasBarOnThisMonitor
    readonly property real lockTransitionProgress: GlobalStates.lockBarTransitionProgress
    readonly property bool lockTransitionActive: lockTransitionProgress > 0.01
    readonly property real lockSlideDistance: topPanel.barVertical ? Appearance.sizes.verticalBarWindowWidth + Appearance.rounding.screenRounding : Appearance.sizes.barHeight + Appearance.rounding.screenRounding
    readonly property real lockSlideOffsetX: topPanel.barVertical ? (topPanel.barOnLeft ? -lockSlideDistance : lockSlideDistance) : 0
    readonly property real lockSlideOffsetY: topPanel.barVertical ? 0 : (topPanel.barBottom ? lockSlideDistance : -lockSlideDistance)
    readonly property real lockVisualOpacity: topPanel.usingWrappedFrame ? 1.0 - lockTransitionProgress : 1.0

    BarThemes {
        id: barThemes
    }

    Component {
        id: policiesContentComponent
        Policies.SidebarPoliciesContent {
            scopeRoot: topPanel
        }
    }

    Component {
        id: dashboardContentComponent
        Dashboard.SidebarDashboardContent {}
    }

    readonly property var activeTheme: barThemes.getTheme(Config.options.bar.expressiveColorTheme)
    readonly property bool hasBarOnThisMonitor: GlobalStates.isScreenAllowedForBar(topPanel.screen)
    readonly property bool barVertical: BarPlacement.vertical && hasBarOnThisMonitor
    readonly property bool barBottom: BarPlacement.bottom && hasBarOnThisMonitor
    readonly property bool barOnLeft: barVertical && !barBottom
    readonly property bool barOnRight: barVertical && barBottom
    readonly property bool policiesOnLeft: Config.options.sidebar.position === "default" || Config.options.sidebar.position === "left"
    readonly property string policiesMonitorName: policiesOnLeft ? GlobalStates.effectiveLeftMonitor : GlobalStates.effectiveRightMonitor
    readonly property bool policiesOpenOnMonitor: GlobalStates.policiesPanelOpen && screen.name === topPanel.policiesMonitorName
    readonly property bool policiesRenderedOnLeft: {
        const pos = Config.options.sidebar.position;
        return pos === "default" || (pos === "left" && GlobalStates.policiesPanelOpen && !GlobalStates.dashboardPanelOpen);
    }
    readonly property bool policiesRenderedOnRight: {
        const pos = Config.options.sidebar.position;
        return pos === "inverted" || (pos === "right" && GlobalStates.policiesPanelOpen && !GlobalStates.dashboardPanelOpen);
    }
    readonly property bool policiesActiveOnMonitor: policiesOnLeft ? topPanel.leftSidebarActiveOnMonitor : topPanel.rightSidebarActiveOnMonitor

    function togglePoliciesExtended() {
        GlobalStates.policiesExtended = !GlobalStates.policiesExtended;
    }

    function togglePoliciesDetach() {
        GlobalStates.policiesDetached = !GlobalStates.policiesDetached;
    }

    function togglePoliciesPin() {
        GlobalStates.policiesPinned = !GlobalStates.policiesPinned;
    }

    readonly property bool isDynamicIslandTop: !topPanel.barVertical && !topPanel.barBottom && BarInteraction.cornerStyle === 3 && hasBarOnThisMonitor
    readonly property bool isDynamicIslandBottom: !topPanel.barVertical && topPanel.barBottom && BarInteraction.cornerStyle === 3 && hasBarOnThisMonitor
    readonly property real sidebarTopOffset: isDynamicIslandTop ? (Appearance.sizes.barHeight + Appearance.sizes.hyprlandGapsOut) : ((!topPanel.barVertical && !topPanel.barBottom && (BarInteraction.cornerStyle === 0 || BarInteraction.cornerStyle === 2) && hasBarOnThisMonitor) ? Appearance.sizes.barHeight : 0)
    readonly property real sidebarBottomOffset: isDynamicIslandBottom ? (Appearance.sizes.barHeight + Appearance.sizes.hyprlandGapsOut) : ((!topPanel.barVertical && topPanel.barBottom && (BarInteraction.cornerStyle === 0 || BarInteraction.cornerStyle === 2) && hasBarOnThisMonitor) ? Appearance.sizes.barHeight : 0)

    property real leftSidebarMaskWidth: 0
    property real rightSidebarMaskWidth: 0

    readonly property real leftContentWidth: {
        const pos = Config.options.sidebar.position;
        if (pos === "inverted")
            return GlobalStates.dashboardWidth;
        if (pos === "left")
            return GlobalStates.dashboardPanelOpen ? GlobalStates.dashboardWidth : GlobalStates.policiesWidth;
        return GlobalStates.policiesWidth;
    }

    readonly property real rightContentWidth: {
        const pos = Config.options.sidebar.position;
        if (pos === "inverted")
            return GlobalStates.policiesWidth;
        if (pos === "right")
            return GlobalStates.sidebarLeftOpen ? GlobalStates.policiesWidth : GlobalStates.dashboardWidth;
        return GlobalStates.dashboardWidth;
    }

    Connections {
        target: GlobalStates
        ignoreUnknownSignals: true
        function onLeftSidebarTargetWidthChanged() {
            if (GlobalStates.leftSidebarTargetWidth > 0 && topPanel.leftSidebarWarmOnMonitor) {
                topPanel.leftSidebarMaskWidth = GlobalStates.leftSidebarTargetWidth;
            }
        }
        function onRightSidebarTargetWidthChanged() {
            if (GlobalStates.rightSidebarTargetWidth > 0 && topPanel.rightSidebarWarmOnMonitor) {
                topPanel.rightSidebarMaskWidth = GlobalStates.rightSidebarTargetWidth;
            }
        }
    }

    Component.onCompleted: {
        if (GlobalStates.leftSidebarTargetWidth > 0 && topPanel.leftSidebarWarmOnMonitor) {
            topPanel.leftSidebarMaskWidth = GlobalStates.leftSidebarTargetWidth;
        }
        if (GlobalStates.rightSidebarTargetWidth > 0 && topPanel.rightSidebarWarmOnMonitor) {
            topPanel.rightSidebarMaskWidth = GlobalStates.rightSidebarTargetWidth;
        }
        topPanel.updateFocusGrab();
    }

    readonly property bool leftSidebarOpenOnMonitor: GlobalStates.sidebarLeftOpen && screen.name === GlobalStates.effectiveLeftMonitor
    readonly property bool rightSidebarOpenOnMonitor: GlobalStates.sidebarRightOpen && screen.name === GlobalStates.effectiveRightMonitor
    readonly property bool keepRightSidebarContentLoaded: Config.ready && Config.options.sidebar.keepRightSidebarLoaded
    readonly property bool rightSidebarContentWanted: GlobalStates.sidebarRightOpen || topPanel.keepRightSidebarContentLoaded
    readonly property bool keepLeftSidebarContentLoaded: Config.ready && Config.options.sidebar.keepLeftSidebarLoaded
    readonly property bool leftSidebarContentWanted: GlobalStates.sidebarLeftOpen || topPanel.keepLeftSidebarContentLoaded
    readonly property bool leftSidebarActiveOnMonitor: (GlobalStates.animatedLeftSidebarWidth > 0 || GlobalStates.sidebarLeftOpen) && screen.name === GlobalStates.effectiveLeftMonitor && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnLeft)
    readonly property bool rightSidebarActiveOnMonitor: (GlobalStates.animatedRightSidebarWidth > 0 || GlobalStates.sidebarRightOpen) && screen.name === GlobalStates.effectiveRightMonitor && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnRight)

    readonly property bool leftSidebarDialogDimmed: leftSidebarContentLoader.status === Loader.Ready && leftSidebarContentLoader.item && leftSidebarContentLoader.item.hasOwnProperty("anyDialogVisible") && leftSidebarContentLoader.item.anyDialogVisible
    readonly property bool rightSidebarDialogDimmed: rightSidebarContentLoader.status === Loader.Ready && rightSidebarContentLoader.item && rightSidebarContentLoader.item.hasOwnProperty("anyDialogVisible") && rightSidebarContentLoader.item.anyDialogVisible

    readonly property color leftSidebarCornerColor: {
        var base = Qt.color(Config.options.bar.expressiveColors ? topPanel.activeTheme.barBackground : Appearance.colors.colLayer0);
        if (!leftSidebarDialogDimmed)
            return base;
        var scrim = Qt.color(Appearance.colors.colScrim);
        return Qt.rgba(base.r * (1 - scrim.a) + scrim.r * scrim.a, base.g * (1 - scrim.a) + scrim.g * scrim.a, base.b * (1 - scrim.a) + scrim.b * scrim.a, base.a);
    }
    readonly property color rightSidebarCornerColor: {
        var base = Qt.color(Config.options.bar.expressiveColors ? topPanel.activeTheme.barBackground : Appearance.colors.colLayer0);
        if (!rightSidebarDialogDimmed)
            return base;
        var scrim = Qt.color(Appearance.colors.colScrim);
        return Qt.rgba(base.r * (1 - scrim.a) + scrim.r * scrim.a, base.g * (1 - scrim.a) + scrim.g * scrim.a, base.b * (1 - scrim.a) + scrim.b * scrim.a, base.a);
    }
    readonly property bool searchDropSuppressed: (Config.ready && Config.options.bar.dynamicIsland.notchMode.enable) || GlobalStates.floatingNotchOwnsSearch || !GlobalStates.searchConnectActive
    readonly property bool searchOpenOnMonitor: (GlobalStates.overviewOpen || (searchDropLoader.item && searchDropLoader.item.openProgress > 0.001)) && GlobalStates.searchConnectActive && screen.name === GlobalStates.activeSearchMonitor && !topPanel.searchDropSuppressed
    readonly property bool osdOpenOnMonitor: GlobalStates.osdVolumeOpen && GlobalStates.osdConnectActive && !(Config.ready && (Config.options.osd.style === "minimalist" || Config.options.osd.style === "material")) && !(Config.ready && BarInteraction.cornerStyle === 3) && screen.name === (Quickshell.screens.find(s => s.name === Hyprland.focusedMonitor?.name) ?? Quickshell.screens[0])?.name && !(Config.ready && (Config.options.bar.floatingNotch.enable || Config.options.bar.floatingNotch.centerInBar))

    readonly property bool hasFullscreenWindowOnMonitor: {
        const monitorData = HyprlandData.monitors.find(m => m.name === topPanel.screen.name);
        const specialWsName = monitorData?.specialWorkspace?.name;
        const workspaces = Hyprland.workspaces.values.filter(w => w.monitor && w.monitor.name === topPanel.screen.name);
        return workspaces.some(workspace => {
            const isWorkspaceActive = workspace.active || (specialWsName && specialWsName !== "" && (workspace.name === specialWsName || workspace.name === "special:" + specialWsName || (specialWsName === "special:special" && workspace.name === "special") || (specialWsName === "special" && workspace.name === "special:special")));

            return isWorkspaceActive && workspace.toplevels.values.some(toplevel => toplevel.wayland && toplevel.wayland.fullscreen);
        });
    }

    readonly property bool leftSidebarWarmOnMonitor: {
        if (GlobalStates.policiesDetached && topPanel.policiesRenderedOnLeft)
            return false;
        if (GlobalStates.effectiveLeftMonitor !== "") {
            return screen.name === GlobalStates.effectiveLeftMonitor;
        }
        return false;
    }
    readonly property bool rightSidebarWarmOnMonitor: {
        if (GlobalStates.policiesDetached && topPanel.policiesRenderedOnRight)
            return false;
        if (GlobalStates.effectiveRightMonitor !== "") {
            return screen.name === GlobalStates.effectiveRightMonitor;
        }
        return false;
    }

    onLeftSidebarActiveOnMonitorChanged: {
        // Debug removed for production performance
    }

    onRightSidebarActiveOnMonitorChanged: {
        // Debug removed for production performance
    }

    readonly property bool barMustShow: {
        if (!barVertical) {
            return horizontalBarLoader.item ? horizontalBarLoader.item.mustShow : false;
        } else {
            return verticalBarLoader.item ? verticalBarLoader.item.mustShow : false;
        }
    }

    readonly property real hBarHiddenAmount: horizontalBarLoader.item ? horizontalBarLoader.item.hiddenAmount : 0
    readonly property real vBarHiddenAmount: verticalBarLoader.item ? verticalBarLoader.item.hiddenAmount : 0

    // Float bar gaps: the bar is visually offset from screen edges by hyprlandGapsOut.
    // SearchDrop/OsdDrop need this offset so they emerge from the bar's visual top edge.
    readonly property real barMargin: BarInteraction.cornerStyle === 1 ? Appearance.sizes.hyprlandGapsOut : 0

    // ── Shell edge slide ─────────────────────────────────────────────────────
    // Fullscreen and media mode used to drop the bar and the frame in a single
    // frame — `active` went false and the panels were simply gone. They now
    // leave through their own edge: the bar slides out past it, the frame folds
    // into the screen edges (WrappedFrameVisuals.hideProgress), and the loaders
    // are only torn down once the slide has finished. The placement swap rides
    // the same offset, so a bar that changes edge exits through the old one and
    // enters through the new one — the direction flips with the config, which
    // GlobalStates writes while we are off screen.
    readonly property bool mediaModeHere: GlobalStates.isMediaModeActiveForScreen(topPanel.screen ? topPanel.screen.name : "")
    readonly property bool shellHiddenWanted: (topPanel.hasFullscreenWindowOnMonitor
            && !GlobalStates.overviewOpen && !GlobalStates.sidebarLeftOpen && !GlobalStates.sidebarRightOpen)
        || topPanel.mediaModeHere
    property real shellHideProgress: topPanel.shellHiddenWanted ? 1 : 0
    Behavior on shellHideProgress {
        animation: Appearance.animation.shellEdgeSlide.numberAnimation.createObject(topPanel)
    }
    readonly property real shellHide: Math.max(shellHideProgress, GlobalStates.barPlacementSwapProgress)
    readonly property bool shellSeated: topPanel.shellHide < 0.999
    // Loaders outlive the hide request by exactly one slide, so there is
    // something on screen to animate out.
    readonly property bool shellContentWanted: !topPanel.mediaModeHere || topPanel.shellSeated
    readonly property real shellSlideY: topPanel.barVertical
        ? 0
        : (topPanel.barBottom ? 1 : -1) * topPanel.shellHide * (Appearance.sizes.barHeight + Appearance.rounding.screenRounding)
    readonly property real shellSlideX: topPanel.barVertical
        ? (topPanel.barOnRight ? 1 : -1) * topPanel.shellHide * (Appearance.sizes.verticalBarWindowWidth + Appearance.rounding.screenRounding)
        : 0

    readonly property bool leftSidebarNeedsKeyboard: leftSidebarOpenOnMonitor && !GlobalStates.connectSidebarsSeparate && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnLeft)
    readonly property bool rightSidebarNeedsKeyboard: rightSidebarOpenOnMonitor && !GlobalStates.connectSidebarsSeparate && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnRight)
    readonly property bool policiesNeedsKeyboard: topPanel.policiesOpenOnMonitor && !GlobalStates.connectSidebarsSeparate && !GlobalStates.policiesDetached

    WlrLayershell.keyboardFocus: (searchOpenOnMonitor || policiesNeedsKeyboard || leftSidebarNeedsKeyboard || rightSidebarNeedsKeyboard) ? WlrKeyboardFocus.OnDemand : WlrKeyboardFocus.None

    // Resolve policy commands at window level so focused/selected TextEdits cannot
    // consume Ctrl+D before the sidebar controller sees it.
    Shortcut {
        sequence: "Ctrl+D"
        enabled: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && topPanel.policiesOpenOnMonitor && !GlobalStates.policiesDetached
        onActivated: topPanel.togglePoliciesDetach()
    }
    Shortcut {
        sequence: "Ctrl+O"
        enabled: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && topPanel.policiesOpenOnMonitor && !GlobalStates.policiesDetached
        onActivated: topPanel.togglePoliciesExtended()
    }
    Shortcut {
        sequence: "Ctrl+P"
        enabled: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && topPanel.policiesOpenOnMonitor && !GlobalStates.policiesDetached
        onActivated: topPanel.togglePoliciesPin()
    }

    // 1. Wrapped Frame Visuals
    Loader {
        id: frameLoader
        active: topPanel.usingWrappedFrame && topPanel.shellContentWanted
        visible: topPanel.shellSeated
        anchors.fill: parent
        opacity: topPanel.lockVisualOpacity
        sourceComponent: Frame.WrappedFrameVisuals {
            hideProgress: topPanel.shellHide
            showBarBackground: horizontalBarLoader.item ? horizontalBarLoader.item.showBarBackground : (verticalBarLoader.item ? verticalBarLoader.item.showBarBackground : false)
            screen: topPanel.screen

            // Plain bindings, not new properties: WrappedFrameVisuals already
            // declares both, and redeclaring them here shadowed the originals so
            // the frame (and now the shell shadow silhouette) never saw the bar
            // retract under autohide.
            hBarHiddenAmount: topPanel.hBarHiddenAmount
            vBarHiddenAmount: topPanel.vBarHiddenAmount

            leftSidebarMaskOffset: topPanel.leftSidebarMaskWidth
            rightSidebarMaskOffset: topPanel.rightSidebarMaskWidth

            sidebarTopOffset: topPanel.sidebarTopOffset
            sidebarBottomOffset: topPanel.sidebarBottomOffset
        }
    }

    // 2. Horizontal Bar Visual Layer
    Loader {
        id: horizontalBarLoader
        active: !topPanel.barVertical && GlobalStates.barOpen && hasBarOnThisMonitor && topPanel.shellContentWanted
        visible: topPanel.shellSeated
        anchors.fill: parent
        opacity: topPanel.lockVisualOpacity
        transform: Translate {
            y: (topPanel.usingWrappedFrame ? 0 : topPanel.lockSlideOffsetY * topPanel.lockTransitionProgress) + topPanel.shellSlideY
        }
        sourceComponent: Component {
            Item {
                id: hBarItem
                anchors.fill: parent

                property int monitorIndex: Quickshell.screens.indexOf(topPanel.screen)
                property bool hasActiveWindows: false
                property bool showBarBackground: (hasActiveWindows && Config.options.bar.barBackgroundStyle === 2) || Config.options.bar.barBackgroundStyle === 1 || Config.options.bar.barBackgroundStyle === 3

                Connections {
                    enabled: Config.options.bar.barBackgroundStyle === 2
                    target: HyprlandData
                    function onWindowListChanged() {
                        const monitor = HyprlandData.monitors.find(m => m.name === topPanel.screen.name);
                        const wsId = monitor?.activeWorkspace?.id;
                        const hasWindow = wsId ? HyprlandData.windowList.some(w => w.workspace.id === wsId && !w.floating) : false;
                        hBarItem.hasActiveWindows = hasWindow;
                    }
                }

                Timer {
                    id: showBarTimer
                    interval: (Config?.options.bar.autoHide.showWhenPressingSuper.delay ?? 100)
                    repeat: false
                    onTriggered: hBarItem.superShow = true
                }

                Connections {
                    target: GlobalStates
                    function onSuperDownChanged() {
                        if (!Config?.options.bar.autoHide.showWhenPressingSuper.enable)
                            return;
                        if (GlobalStates.superDown)
                            showBarTimer.restart();
                        else {
                            showBarTimer.stop();
                            hBarItem.superShow = false;
                        }
                    }
                }

                // ── Hover delay trigger ───────────────────────────────────────
                property bool hoverTriggered: false
                readonly property int hoverDelay: Config?.options.bar.autoHide.hoverDelay ?? 0

                Timer {
                    id: hoverOpenTimer
                    interval: hBarItem.hoverDelay
                    repeat: false
                    onTriggered: hBarItem.hoverTriggered = true
                }

                Connections {
                    target: hoverRegion
                    function onContainsMouseChanged() {
                        if (hoverRegion.containsMouse) {
                            if (hBarItem.hoverDelay <= 0 || (Config?.options.bar.autoHide.enable && !hBarItem.mustShow) === false || hBarItem.superShow || topPanel.leftSidebarOpenOnMonitor || topPanel.rightSidebarOpenOnMonitor) {
                                hBarItem.hoverTriggered = true;
                            } else {
                                hoverOpenTimer.restart();
                            }
                        } else {
                            hoverOpenTimer.stop();
                            hBarItem.hoverTriggered = false;
                        }
                    }
                }

                property bool superShow: false
                property bool mustShow: hoverTriggered || superShow || topPanel.leftSidebarOpenOnMonitor || topPanel.rightSidebarOpenOnMonitor

                MouseArea {
                    id: hoverRegion
                    hoverEnabled: true
                    anchors {
                        left: parent.left
                        right: parent.right
                        top: !topPanel.barBottom ? parent.top : undefined
                        bottom: topPanel.barBottom ? parent.bottom : undefined
                        rightMargin: (Config.options.interactions.deadPixelWorkaround.enable) * 1
                        bottomMargin: (Config.options.interactions.deadPixelWorkaround.enable && topPanel.barBottom) * 1
                    }
                    height: Appearance.sizes.barHeight + Appearance.rounding.screenRounding

                    Item {
                        id: hoverMaskRegion
                        anchors {
                            fill: barContent
                            topMargin: -Config.options.bar.autoHide.hoverRegionWidth
                            bottomMargin: -Config.options.bar.autoHide.hoverRegionWidth
                        }
                    }

                    Bar.BarContent {
                        id: barContent
                        monitorIndex: hBarItem.monitorIndex
                        implicitHeight: Appearance.sizes.barHeight
                        anchors {
                            right: parent.right
                            left: parent.left
                            top: parent.top
                            bottom: undefined
                            topMargin: (Config?.options.bar.autoHide.enable && !hBarItem.mustShow) ? -Appearance.sizes.barHeight : 0
                            rightMargin: (Config.options.interactions.deadPixelWorkaround.enable) * -1
                        }

                        Behavior on anchors.topMargin {
                            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(barContent)
                        }
                        Behavior on anchors.bottomMargin {
                            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(barContent)
                        }

                        states: State {
                            name: "bottom"
                            when: topPanel.barBottom
                            AnchorChanges {
                                target: barContent
                                anchors {
                                    right: parent.right
                                    left: parent.left
                                    top: undefined
                                    bottom: parent.bottom
                                }
                            }
                            PropertyChanges {
                                target: barContent
                                anchors.topMargin: 0
                                anchors.bottomMargin: (Config?.options.bar.autoHide.enable && !hBarItem.mustShow) ? -Appearance.sizes.barHeight : (Config.options.interactions.deadPixelWorkaround.enable) * -1
                            }
                        }
                    }

                    Loader {
                        id: roundDecorators
                        anchors {
                            left: parent.left
                            right: parent.right
                            top: barContent.bottom
                            bottom: undefined
                        }
                        height: Appearance.rounding.screenRounding
                        active: hBarItem.showBarBackground && BarInteraction.cornerStyle === 0 && !topPanel.usingWrappedFrame

                        states: State {
                            name: "bottom"
                            when: topPanel.barBottom
                            AnchorChanges {
                                target: roundDecorators
                                anchors {
                                    right: parent.right
                                    left: parent.left
                                    top: undefined
                                    bottom: barContent.top
                                }
                            }
                        }

                        sourceComponent: Item {
                            implicitHeight: Appearance.rounding.screenRounding
                            RoundCorner {
                                id: leftCorner
                                anchors {
                                    top: parent.top
                                    bottom: parent.bottom
                                    left: parent.left
                                    leftMargin: topPanel.leftSidebarActiveOnMonitor ? GlobalStates.animatedLeftSidebarWidth : 0
                                }
                                implicitSize: Appearance.rounding.screenRounding
                                color: hBarItem.showBarBackground ? (Config.options.bar.expressiveColors ? topPanel.activeTheme.barBackground : Appearance.colors.colLayer0) : "transparent"
                                corner: RoundCorner.CornerEnum.TopLeft
                                states: State {
                                    name: "bottom"
                                    when: topPanel.barBottom
                                    PropertyChanges {
                                        target: leftCorner
                                        corner: RoundCorner.CornerEnum.BottomLeft
                                    }
                                }
                            }
                            RoundCorner {
                                id: rightCorner
                                anchors {
                                    top: !topPanel.barBottom ? parent.top : undefined
                                    bottom: topPanel.barBottom ? parent.bottom : undefined
                                    right: parent.right
                                    rightMargin: topPanel.rightSidebarActiveOnMonitor ? GlobalStates.animatedRightSidebarWidth : 0
                                }
                                implicitSize: Appearance.rounding.screenRounding
                                color: hBarItem.showBarBackground ? (Config.options.bar.expressiveColors ? topPanel.activeTheme.barBackground : Appearance.colors.colLayer0) : "transparent"
                                corner: RoundCorner.CornerEnum.TopRight
                                states: State {
                                    name: "bottom"
                                    when: topPanel.barBottom
                                    PropertyChanges {
                                        target: rightCorner
                                        corner: RoundCorner.CornerEnum.BottomRight
                                    }
                                }
                            }
                        }
                    }
                }

                property alias maskItem: hoverMaskRegion
                property real hiddenAmount: (Config?.options.bar.autoHide.enable && !mustShow) ? Appearance.sizes.barHeight : 0

                Behavior on hiddenAmount {
                    animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(hBarItem)
                }
            }
        }
    }

    // 3. Vertical Bar Visual Layer
    Loader {
        id: verticalBarLoader
        active: topPanel.barVertical && GlobalStates.barOpen && hasBarOnThisMonitor && topPanel.shellContentWanted
        visible: topPanel.shellSeated
        anchors.fill: parent
        opacity: topPanel.lockVisualOpacity
        transform: Translate {
            x: (topPanel.usingWrappedFrame ? 0 : topPanel.lockSlideOffsetX * topPanel.lockTransitionProgress) + topPanel.shellSlideX
        }
        sourceComponent: Component {
            Item {
                id: vBarItem
                anchors.fill: parent

                property int monitorIndex: Quickshell.screens.indexOf(topPanel.screen)
                property bool hasActiveWindows: false
                property bool showBarBackground: (hasActiveWindows && Config.options.bar.barBackgroundStyle === 2) || Config.options.bar.barBackgroundStyle === 1 || Config.options.bar.barBackgroundStyle === 3

                Connections {
                    enabled: Config.options.bar.barBackgroundStyle === 2
                    target: HyprlandData
                    function onWindowListChanged() {
                        const monitor = HyprlandData.monitors.find(m => m.name === topPanel.screen.name);
                        const wsId = monitor?.activeWorkspace?.id;
                        const hasWindow = wsId ? HyprlandData.windowList.some(w => w.workspace.id === wsId && !w.floating) : false;
                        vBarItem.hasActiveWindows = hasWindow;
                    }
                }

                Timer {
                    id: showBarTimer
                    interval: (Config?.options.bar.autoHide.showWhenPressingSuper.delay ?? 100)
                    repeat: false
                    onTriggered: vBarItem.superShow = true
                }

                Connections {
                    target: GlobalStates
                    function onSuperDownChanged() {
                        if (!Config?.options.bar.autoHide.showWhenPressingSuper.enable)
                            return;
                        if (GlobalStates.superDown)
                            showBarTimer.restart();
                        else {
                            showBarTimer.stop();
                            vBarItem.superShow = false;
                        }
                    }
                }

                // ── Hover delay trigger ───────────────────────────────────────
                property bool hoverTriggered: false
                readonly property int hoverDelay: Config?.options.bar.autoHide.hoverDelay ?? 0

                Timer {
                    id: hoverOpenTimer
                    interval: vBarItem.hoverDelay
                    repeat: false
                    onTriggered: vBarItem.hoverTriggered = true
                }

                Connections {
                    target: hoverRegion
                    function onContainsMouseChanged() {
                        if (hoverRegion.containsMouse) {
                            if (vBarItem.hoverDelay <= 0 || (Config?.options.bar.autoHide.enable && !vBarItem.mustShow) === false || vBarItem.superShow || topPanel.leftSidebarOpenOnMonitor || topPanel.rightSidebarOpenOnMonitor) {
                                vBarItem.hoverTriggered = true;
                            } else {
                                hoverOpenTimer.restart();
                            }
                        } else {
                            hoverOpenTimer.stop();
                            vBarItem.hoverTriggered = false;
                        }
                    }
                }

                property bool superShow: false
                property bool mustShow: hoverTriggered || superShow || topPanel.leftSidebarOpenOnMonitor || topPanel.rightSidebarOpenOnMonitor

                MouseArea {
                    id: hoverRegion
                    hoverEnabled: true
                    anchors.fill: parent

                    Item {
                        id: hoverMaskRegion
                        anchors {
                            fill: barContent
                            leftMargin: -Config.options.bar.autoHide.hoverRegionWidth
                            rightMargin: -Config.options.bar.autoHide.hoverRegionWidth
                        }
                    }

                    VBar.VerticalBarContent {
                        id: barContent
                        monitorIndex: vBarItem.monitorIndex
                        implicitWidth: Appearance.sizes.verticalBarWindowWidth
                        width: implicitWidth
                        anchors {
                            top: parent.top
                            bottom: parent.bottom
                            left: undefined
                            right: undefined
                        }

                        x: {
                            if (topPanel.barOnLeft) {
                                let hide = (Config?.options.bar.autoHide.enable && !vBarItem.mustShow) ? -Appearance.sizes.verticalBarWindowWidth : 0;
                                let push = (topPanel.leftSidebarActiveOnMonitor) ? GlobalStates.animatedLeftSidebarWidth : 0;
                                return hide + push;
                            } else if (topPanel.barOnRight) {
                                let hide = (Config?.options.bar.autoHide.enable && !vBarItem.mustShow) ? Appearance.sizes.verticalBarWindowWidth : 0;
                                let push = (topPanel.rightSidebarActiveOnMonitor) ? GlobalStates.animatedRightSidebarWidth : 0;
                                return parent.width - width + hide - push;
                            }
                            return 0;
                        }

                        Behavior on x {
                            enabled: !GlobalStates.sidebarLeftOpen && !GlobalStates.sidebarRightOpen && GlobalStates.animatedLeftSidebarWidth === 0 && GlobalStates.animatedRightSidebarWidth === 0
                            animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(barContent)
                        }
                    }

                    Loader {
                        id: roundDecorators
                        anchors {
                            top: parent.top
                            bottom: parent.bottom
                            left: barContent.right
                            right: undefined
                        }
                        width: Appearance.rounding.screenRounding
                        active: vBarItem.showBarBackground && BarInteraction.cornerStyle === 0 && !topPanel.usingWrappedFrame

                        states: State {
                            name: "right"
                            when: topPanel.barBottom
                            AnchorChanges {
                                target: roundDecorators
                                anchors {
                                    top: parent.top
                                    bottom: parent.bottom
                                    left: undefined
                                    right: barContent.left
                                }
                            }
                        }

                        sourceComponent: Item {
                            implicitWidth: Appearance.rounding.screenRounding
                            RoundCorner {
                                id: topCorner
                                anchors {
                                    left: parent.left
                                    right: parent.right
                                    top: parent.top
                                }
                                implicitSize: Appearance.rounding.screenRounding
                                color: vBarItem.showBarBackground ? (Config.options.bar.expressiveColors ? topPanel.activeTheme.barBackground : Appearance.colors.colLayer0) : "transparent"
                                corner: RoundCorner.CornerEnum.TopLeft
                                states: State {
                                    name: "bottom"
                                    when: topPanel.barBottom
                                    PropertyChanges {
                                        target: topCorner
                                        corner: RoundCorner.CornerEnum.TopRight
                                    }
                                }
                            }
                            RoundCorner {
                                id: bottomCorner
                                anchors {
                                    bottom: parent.bottom
                                    left: !topPanel.barBottom ? parent.left : undefined
                                    right: topPanel.barBottom ? parent.right : undefined
                                }
                                implicitSize: Appearance.rounding.screenRounding
                                color: vBarItem.showBarBackground ? (Config.options.bar.expressiveColors ? topPanel.activeTheme.barBackground : Appearance.colors.colLayer0) : "transparent"
                                corner: RoundCorner.CornerEnum.BottomLeft
                                states: State {
                                    name: "bottom"
                                    when: topPanel.barBottom
                                    PropertyChanges {
                                        target: bottomCorner
                                        corner: RoundCorner.CornerEnum.BottomRight
                                    }
                                }
                            }
                        }
                    }
                }

                property alias maskItem: hoverMaskRegion
                property real hiddenAmount: (Config?.options.bar.autoHide.enable && !mustShow) ? Appearance.sizes.verticalBarWindowWidth : 0

                Behavior on hiddenAmount {
                    animation: Appearance.animation.elementMoveFast.numberAnimation.createObject(vBarItem)
                }
            }
        }
    }

    Loader {
        id: leftSidebarShadowLoader
        active: topPanel.leftSidebarActiveOnMonitor && (!GlobalStates.connectModeActive || topPanel.isDynamicIslandTop || topPanel.isDynamicIslandBottom)
        anchors.fill: leftSidebar
        sourceComponent: Component {
            StyledDropShadow {
                target: leftSidebar
                radius: Math.round(0.9 * Appearance.sizes.elevationMargin)
                opacity: leftSidebar.opacity
            }
        }
    }

    // Space reserver for pinned sidebar in Connect Mode
    // Disabled in Float+Connect mode — sidebars remain separate PanelWindows with own space
    PanelWindow {
        id: pinSpaceReserver
        WlrLayershell.namespace: "quickshell:pinReserver"
        exclusionMode: ExclusionMode.Normal
        color: "transparent"
        visible: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && GlobalStates.policiesPinned && !GlobalStates.policiesDetached && topPanel.policiesActiveOnMonitor
        anchors {
            top: true
            bottom: true
            left: topPanel.policiesOnLeft
            right: !topPanel.policiesOnLeft
        }
        implicitWidth: GlobalStates.policiesWidth
        exclusiveZone: implicitWidth - ((topPanel.barVertical && ((topPanel.barOnLeft && topPanel.policiesOnLeft) || (topPanel.barOnRight && !topPanel.policiesOnLeft))) ? 0 : (Appearance.sizes.hyprlandGapsOut + Appearance.sizes.elevationMargin))
    }

    // Left Sidebar Policies Content
    // Disabled in Float+Connect mode (cornerStyle 1) — sidebars remain separate PanelWindows
    Rectangle {
        id: leftSidebar
        x: -(width - GlobalStates.animatedLeftSidebarWidth)
        y: topPanel.sidebarTopOffset
        width: Math.round(Math.max(topPanel.leftContentWidth, GlobalStates.animatedLeftSidebarWidth))
        height: Math.max(0, Math.round(parent.height - topPanel.sidebarTopOffset - topPanel.sidebarBottomOffset))
        color: Config.options.bar.expressiveColors ? activeTheme.barBackground : Appearance.colors.colLayer0
        border.width: GlobalStates.connectModeActive ? 0 : 1
        border.color: GlobalStates.connectModeActive ? "transparent" : Appearance.colors.colLayer0Border
        readonly property bool isConnectDynamicIslandTop: GlobalStates.connectModeActive && topPanel.isDynamicIslandTop
        readonly property bool isConnectDynamicIslandBottom: GlobalStates.connectModeActive && topPanel.isDynamicIslandBottom
        readonly property real defaultRadius: (GlobalStates.connectModeActive && !topPanel.isDynamicIslandTop && !topPanel.isDynamicIslandBottom) ? 0 : Appearance.rounding.screenRounding - Appearance.sizes.hyprlandGapsOut + 1
        radius: 0
        topLeftRadius: isConnectDynamicIslandTop ? 0 : defaultRadius
        topRightRadius: isConnectDynamicIslandBottom ? 0 : defaultRadius
        bottomLeftRadius: (isConnectDynamicIslandBottom) ? 0 : (GlobalStates.connectModeActive ? 0 : defaultRadius)
        bottomRightRadius: isConnectDynamicIslandBottom ? defaultRadius : (GlobalStates.connectModeActive ? 0 : defaultRadius)
        visible: topPanel.leftSidebarActiveOnMonitor && !GlobalStates.connectSidebarsSeparate

        // GPU compositing during animation: prevents per-frame mask/Region recalc
        // which was causing Wayland surface sync stalls on every animation frame.
        // Only active DURING the open/close animation — not while the sidebar is
        // statically open. Keeping it on while open caused massive CPU usage
        // because every minor visual change (timer ticks, notification syncs,
        // infinite pulse animations, gradient behaviors) forced a full FBO
        // re-render of the entire Phone tab subtree.
        layer.enabled: GlobalStates.leftSidebarAnimating

        Loader {
            id: leftSidebarContentLoader
            active: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarContentWanted && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnLeft)
            anchors.fill: parent
            sourceComponent: {
                const pos = Config.options.sidebar.position;
                if (pos === "inverted") {
                    return dashboardContentComponent;
                } else if (pos === "left") {
                    if (GlobalStates.dashboardPanelOpen) {
                        return dashboardContentComponent;
                    } else {
                        return policiesContentComponent;
                    }
                } else {
                    return policiesContentComponent;
                }
            }
            onLoaded: {
                if (item && "isLoadedOnLeft" in item) {
                    item.isLoadedOnLeft = true;
                }
            }
        }
    }

    // Detached Sidebar Policies Window
    // Disabled in Float+Connect mode — sidebars remain separate PanelWindows
    Loader {
        active: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && GlobalStates.policiesDetached && topPanel.policiesOpenOnMonitor
        sourceComponent: FloatingWindow {
            id: detachedPoliciesWindow
            title: "ii Policies"
            screen: topPanel.screen
            color: "transparent"
            visible: true
            implicitWidth: GlobalStates.policiesWidth
            implicitHeight: Math.max(0, topPanel.height - topPanel.sidebarTopOffset - topPanel.sidebarBottomOffset - (Appearance.sizes.hyprlandGapsOut * 2))

            Shortcut {
                sequence: "Ctrl+D"
                enabled: detachedPoliciesWindow.visible
                onActivated: topPanel.togglePoliciesDetach()
            }
            Shortcut {
                sequence: "Ctrl+O"
                enabled: detachedPoliciesWindow.visible
                onActivated: topPanel.togglePoliciesExtended()
            }
            Shortcut {
                sequence: "Ctrl+P"
                enabled: detachedPoliciesWindow.visible
                onActivated: topPanel.togglePoliciesPin()
            }

            onVisibleChanged: {
                if (!visible && GlobalStates.sidebarLeftOpen)
                    GlobalStates.sidebarLeftOpen = false;
            }

            Rectangle {
                anchors.fill: parent
                focus: true
                color: Config.options.bar.expressiveColors ? activeTheme.barBackground : Appearance.colors.colLayer0
                radius: Appearance.rounding.screenRounding - Appearance.sizes.hyprlandGapsOut + 1
                border.width: 1
                border.color: Appearance.colors.colLayer0Border

                Loader {
                    anchors.fill: parent
                    active: true
                    sourceComponent: policiesContentComponent
                }

                Keys.onPressed: event => {
                    if (event.key === Qt.Key_Escape) {
                        GlobalStates.sidebarLeftOpen = false;
                        event.accepted = true;
                        return;
                    }
                    if ((event.modifiers & Qt.ControlModifier) !== 0) {
                        if (event.key === Qt.Key_D) {
                            topPanel.togglePoliciesDetach();
                        } else if (event.key === Qt.Key_O) {
                            topPanel.togglePoliciesExtended();
                        } else if (event.key === Qt.Key_P) {
                            topPanel.togglePoliciesPin();
                        } else {
                            return;
                        }
                        event.accepted = true;
                    }
                }
            }
        }
    }

    // Right Sidebar Dashboard Content
    // Disabled in Float+Connect mode (cornerStyle 1) — sidebars remain separate PanelWindows
    Rectangle {
        id: rightSidebar
        x: parent.width - Math.round(GlobalStates.animatedRightSidebarWidth)
        y: topPanel.sidebarTopOffset
        width: Math.round(Math.max(topPanel.rightContentWidth, GlobalStates.animatedRightSidebarWidth))
        height: Math.max(0, Math.round(parent.height - topPanel.sidebarTopOffset - topPanel.sidebarBottomOffset))
        color: Config.options.bar.expressiveColors ? activeTheme.barBackground : Appearance.colors.colLayer0
        border.width: GlobalStates.connectModeActive ? 0 : 1
        border.color: GlobalStates.connectModeActive ? "transparent" : Appearance.colors.colLayer0Border
        readonly property bool isConnectDynamicIslandTop: GlobalStates.connectModeActive && topPanel.isDynamicIslandTop
        readonly property bool isConnectDynamicIslandBottom: GlobalStates.connectModeActive && topPanel.isDynamicIslandBottom
        readonly property real defaultRadius: (GlobalStates.connectModeActive && !topPanel.isDynamicIslandTop && !topPanel.isDynamicIslandBottom) ? 0 : Appearance.rounding.screenRounding - Appearance.sizes.hyprlandGapsOut + 1
        radius: 0
        topRightRadius: isConnectDynamicIslandTop ? 0 : defaultRadius
        topLeftRadius: isConnectDynamicIslandBottom ? 0 : defaultRadius
        bottomRightRadius: (isConnectDynamicIslandBottom) ? 0 : (GlobalStates.connectModeActive ? 0 : defaultRadius)
        bottomLeftRadius: isConnectDynamicIslandBottom ? defaultRadius : (GlobalStates.connectModeActive ? 0 : defaultRadius)
        visible: topPanel.rightSidebarActiveOnMonitor && (!topPanel.hasFullscreenWindowOnMonitor || topPanel.rightSidebarActiveOnMonitor) && !GlobalStates.connectSidebarsSeparate

        // GPU compositing during animation: prevents per-frame mask/Region recalc
        // which was causing Wayland surface sync stalls on every animation frame.
        // Only active DURING the open/close animation — not while the sidebar is
        // statically open. Keeping it on while open caused visible seam artifacts
        // at the corner junctions because the FBO edge anti-aliasing differs from
        // direct rendering of the RoundCorner overlays.
        layer.enabled: GlobalStates.rightSidebarAnimating

        Loader {
            id: rightSidebarContentLoader
            active: GlobalStates.connectModeActive && !GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarContentWanted && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnRight)
            anchors.fill: parent
            sourceComponent: {
                const pos = Config.options.sidebar.position;
                if (pos === "inverted") {
                    return policiesContentComponent;
                } else if (pos === "right") {
                    if (GlobalStates.sidebarLeftOpen) {
                        return policiesContentComponent;
                    } else {
                        return dashboardContentComponent;
                    }
                } else {
                    return dashboardContentComponent;
                }
            }
            onLoaded: {
                if (item && "isLoadedOnLeft" in item) {
                    item.isLoadedOnLeft = false;
                }
            }
        }
    }

    Loader {
        id: leftSidebarTopCornerLoader
        active: topPanel.leftSidebarActiveOnMonitor && BarInteraction.cornerStyle !== 1 && !topPanel.isDynamicIslandTop && !topPanel.usingWrappedFrame && (topPanel.barBottom || BarInteraction.cornerStyle !== 0 || !hasBarOnThisMonitor)
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.leftSidebarOpenOnMonitor
        x: GlobalStates.animatedLeftSidebarWidth
        y: topPanel.sidebarTopOffset
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.TopLeft
            color: topPanel.leftSidebarCornerColor
        }
    }

    Loader {
        id: leftSidebarOuterTopCornerShadowLoader
        active: leftSidebarOuterTopCornerLoader.active
        anchors.fill: leftSidebarOuterTopCornerLoader
        sourceComponent: Component {
            StyledDropShadow {
                target: leftSidebarOuterTopCornerLoader
                radius: Math.round(0.9 * Appearance.sizes.elevationMargin)
                opacity: leftSidebar.opacity
            }
        }
    }

    Loader {
        id: leftSidebarOuterTopCornerLoader
        active: topPanel.leftSidebarActiveOnMonitor && topPanel.isDynamicIslandTop && !topPanel.usingWrappedFrame
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.leftSidebarOpenOnMonitor
        anchors.left: leftSidebar.left
        anchors.bottom: leftSidebar.top
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.BottomLeft
            color: topPanel.leftSidebarCornerColor
        }
    }

    Loader {
        id: leftSidebarBottomCornerLoader
        active: topPanel.leftSidebarActiveOnMonitor && (!topPanel.barBottom || !hasBarOnThisMonitor) && BarInteraction.cornerStyle !== 1 && !topPanel.usingWrappedFrame && (topPanel.barVertical === topPanel.barBottom || BarInteraction.cornerStyle !== 0 || !hasBarOnThisMonitor)
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.leftSidebarOpenOnMonitor
        x: GlobalStates.animatedLeftSidebarWidth
        anchors.bottom: parent.bottom
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.BottomLeft
            color: topPanel.leftSidebarCornerColor
        }
    }

    Loader {
        id: rightSidebarTopCornerLoader
        active: topPanel.rightSidebarActiveOnMonitor && BarInteraction.cornerStyle !== 1 && !topPanel.isDynamicIslandTop && !topPanel.usingWrappedFrame && (topPanel.barVertical !== topPanel.barBottom || BarInteraction.cornerStyle !== 0 || !hasBarOnThisMonitor)
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.rightSidebarOpenOnMonitor
        anchors.right: parent.right
        anchors.rightMargin: GlobalStates.animatedRightSidebarWidth
        y: topPanel.sidebarTopOffset
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.TopRight
            color: topPanel.rightSidebarCornerColor
        }
    }

    Loader {
        id: rightSidebarOuterTopCornerShadowLoader
        active: rightSidebarOuterTopCornerLoader.active
        anchors.fill: rightSidebarOuterTopCornerLoader
        sourceComponent: Component {
            StyledDropShadow {
                target: rightSidebarOuterTopCornerLoader
                radius: Math.round(0.9 * Appearance.sizes.elevationMargin)
                opacity: rightSidebar.opacity
            }
        }
    }

    Loader {
        id: rightSidebarOuterTopCornerLoader
        active: topPanel.rightSidebarActiveOnMonitor && topPanel.isDynamicIslandTop && !topPanel.usingWrappedFrame
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.rightSidebarOpenOnMonitor
        anchors.right: rightSidebar.right
        anchors.bottom: rightSidebar.top
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.BottomRight
            color: topPanel.rightSidebarCornerColor
        }
    }

    Loader {
        id: rightSidebarBottomCornerLoader
        active: topPanel.rightSidebarActiveOnMonitor && (!topPanel.barBottom || !hasBarOnThisMonitor) && BarInteraction.cornerStyle !== 1 && !topPanel.usingWrappedFrame
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.rightSidebarOpenOnMonitor
        anchors.right: parent.right
        anchors.rightMargin: GlobalStates.animatedRightSidebarWidth
        anchors.bottom: parent.bottom
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.BottomRight
            color: topPanel.rightSidebarCornerColor
        }
    }

    Loader {
        id: leftSidebarBottomBarCornerLoader
        active: topPanel.leftSidebarActiveOnMonitor && !topPanel.barVertical && topPanel.barBottom && (BarInteraction.cornerStyle === 0 || BarInteraction.cornerStyle === 2) && !topPanel.usingWrappedFrame && hasBarOnThisMonitor
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.leftSidebarOpenOnMonitor
        x: GlobalStates.animatedLeftSidebarWidth
        y: parent.height - topPanel.sidebarBottomOffset - Appearance.rounding.screenRounding
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.BottomLeft
            color: topPanel.leftSidebarCornerColor
        }
    }

    Loader {
        id: rightSidebarBottomBarCornerLoader
        active: topPanel.rightSidebarActiveOnMonitor && !topPanel.barVertical && topPanel.barBottom && (BarInteraction.cornerStyle === 0 || BarInteraction.cornerStyle === 2) && !topPanel.usingWrappedFrame && hasBarOnThisMonitor
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.rightSidebarOpenOnMonitor
        anchors.right: parent.right
        anchors.rightMargin: GlobalStates.animatedRightSidebarWidth
        y: parent.height - topPanel.sidebarBottomOffset - Appearance.rounding.screenRounding
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.BottomRight
            color: topPanel.rightSidebarCornerColor
        }
    }

    Loader {
        id: leftSidebarOuterBottomCornerLoader
        active: topPanel.leftSidebarActiveOnMonitor && topPanel.isDynamicIslandBottom && !topPanel.usingWrappedFrame
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.leftSidebarOpenOnMonitor
        anchors.left: leftSidebar.left
        anchors.top: leftSidebar.bottom
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.TopLeft
            color: topPanel.leftSidebarCornerColor
        }
    }

    Loader {
        id: leftSidebarOuterBottomCornerShadowLoader
        active: leftSidebarOuterBottomCornerLoader.active
        anchors.fill: leftSidebarOuterBottomCornerLoader
        sourceComponent: Component {
            StyledDropShadow {
                target: leftSidebarOuterBottomCornerLoader
                radius: Math.round(0.9 * Appearance.sizes.elevationMargin)
                opacity: leftSidebar.opacity
            }
        }
    }

    Loader {
        id: rightSidebarOuterBottomCornerLoader
        active: topPanel.rightSidebarActiveOnMonitor && topPanel.isDynamicIslandBottom && !topPanel.usingWrappedFrame
        visible: !topPanel.hasFullscreenWindowOnMonitor || topPanel.rightSidebarOpenOnMonitor
        anchors.right: rightSidebar.right
        anchors.top: rightSidebar.bottom
        width: Appearance.rounding.screenRounding
        height: Appearance.rounding.screenRounding
        sourceComponent: RoundCorner {
            implicitSize: Appearance.rounding.screenRounding
            corner: RoundCorner.CornerEnum.TopRight
            color: topPanel.rightSidebarCornerColor
        }
    }

    Loader {
        id: rightSidebarOuterBottomCornerShadowLoader
        active: rightSidebarOuterBottomCornerLoader.active
        anchors.fill: rightSidebarOuterBottomCornerLoader
        sourceComponent: Component {
            StyledDropShadow {
                target: rightSidebarOuterBottomCornerLoader
                radius: Math.round(0.9 * Appearance.sizes.elevationMargin)
                opacity: rightSidebar.opacity
            }
        }
    }

    // 4. Search Drop (Connect Mode integration)
    Loader {
        id: searchDropLoader
        z: 10
        active: !GlobalStates.screenLocked && !topPanel.searchDropSuppressed
        focus: searchOpenOnMonitor
        sourceComponent: Component {
            SearchConnect.SearchDrop {
                id: searchDrop
                screen: topPanel.screen
                monitorIndex: Quickshell.screens.indexOf(topPanel.screen)
                panelWindow: topPanel
                barVertical: topPanel.barVertical
                barBottom: topPanel.barBottom
                barOnLeft: topPanel.barOnLeft
                barOnRight: topPanel.barOnRight
                usingWrappedFrame: topPanel.usingWrappedFrame
                frameThickness: Config.options.appearance.wrappedFrameThickness
                barHeight: hasBarOnThisMonitor ? Appearance.sizes.barHeight : 0
                verticalBarWidth: hasBarOnThisMonitor ? Appearance.sizes.verticalBarWindowWidth : 0
                barMargin: topPanel.barMargin
                hBarHiddenAmount: topPanel.hBarHiddenAmount
                vBarHiddenAmount: topPanel.vBarHiddenAmount
                animatedLeftSidebarWidth: GlobalStates.animatedLeftSidebarWidth
                animatedRightSidebarWidth: GlobalStates.animatedRightSidebarWidth
                leftSidebarActiveOnMonitor: topPanel.leftSidebarActiveOnMonitor
                rightSidebarActiveOnMonitor: topPanel.rightSidebarActiveOnMonitor
            }
        }
    }

    // 5. OSD Drop (Connect Mode integration)
    Loader {
        id: osdDropLoader
        z: 11
        active: GlobalStates.osdConnectActive && !GlobalStates.screenLocked && !(Config.ready && (Config.options.osd.style === "minimalist" || Config.options.osd.style === "material")) && !(Config.ready && BarInteraction.cornerStyle === 3) && !(Config.ready && Config.options.bar.dynamicIsland.notchMode.enable) && !(Config.ready && (Config.options.bar.floatingNotch.enable || Config.options.bar.floatingNotch.centerInBar))
        sourceComponent: Component {
            OsdConnect.OsdDrop {
                screen: topPanel.screen
                panelWindow: topPanel
                barVertical: topPanel.barVertical
                barBottom: topPanel.barBottom
                barOnLeft: topPanel.barOnLeft
                barOnRight: topPanel.barOnRight
                usingWrappedFrame: topPanel.usingWrappedFrame
                frameThickness: Config.options.appearance.wrappedFrameThickness
                barHeight: hasBarOnThisMonitor ? Appearance.sizes.barHeight : 0
                verticalBarWidth: hasBarOnThisMonitor ? Appearance.sizes.verticalBarWindowWidth : 0
                barMargin: topPanel.barMargin
                hBarHiddenAmount: topPanel.hBarHiddenAmount
                vBarHiddenAmount: topPanel.vBarHiddenAmount
                animatedLeftSidebarWidth: GlobalStates.animatedLeftSidebarWidth
                animatedRightSidebarWidth: GlobalStates.animatedRightSidebarWidth
                leftSidebarActiveOnMonitor: topPanel.leftSidebarActiveOnMonitor
                rightSidebarActiveOnMonitor: topPanel.rightSidebarActiveOnMonitor
                hasFullscreenWindow: topPanel.hasFullscreenWindowOnMonitor
            }
        }
    }

    // Static items for input masking to avoid per-frame Region recalculations
    Item {
        id: leftSidebarMaskItem
        x: 0
        y: topPanel.sidebarTopOffset
        width: (GlobalStates.animatedLeftSidebarWidth > 0 && topPanel.leftSidebarWarmOnMonitor) ? topPanel.leftSidebarMaskWidth : 0
        height: parent.height - topPanel.sidebarTopOffset - topPanel.sidebarBottomOffset
    }

    Item {
        id: rightSidebarMaskItem
        x: parent.width - width
        y: topPanel.sidebarTopOffset
        width: (GlobalStates.animatedRightSidebarWidth > 0 && topPanel.rightSidebarWarmOnMonitor) ? topPanel.rightSidebarMaskWidth : 0
        height: parent.height - topPanel.sidebarTopOffset - topPanel.sidebarBottomOffset
    }

    // Static corner mask items to prevent per-frame Region recalculation
    Item {
        id: leftSidebarTopCornerMaskItem
        x: topPanel.leftSidebarMaskWidth
        y: 0
        width: leftSidebarTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: leftSidebarTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: leftSidebarOuterTopCornerMaskItem
        x: leftSidebar.x
        y: topPanel.sidebarTopOffset - (leftSidebarOuterTopCornerLoader.active ? Appearance.rounding.screenRounding : 0)
        width: leftSidebarOuterTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: leftSidebarOuterTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: leftSidebarBottomCornerMaskItem
        x: topPanel.leftSidebarMaskWidth
        y: topPanel.height - (leftSidebarBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0)
        width: leftSidebarBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: leftSidebarBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: rightSidebarTopCornerMaskItem
        x: topPanel.width - topPanel.rightSidebarMaskWidth - width
        y: 0
        width: rightSidebarTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: rightSidebarTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: rightSidebarOuterTopCornerMaskItem
        x: rightSidebar.x + rightSidebar.width - width
        y: topPanel.sidebarTopOffset - (rightSidebarOuterTopCornerLoader.active ? Appearance.rounding.screenRounding : 0)
        width: rightSidebarOuterTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: rightSidebarOuterTopCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: rightSidebarBottomCornerMaskItem
        x: topPanel.width - topPanel.rightSidebarMaskWidth - width
        y: topPanel.height - height
        width: rightSidebarBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: rightSidebarBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: leftSidebarBottomBarCornerMaskItem
        x: topPanel.leftSidebarMaskWidth
        y: parent.height - topPanel.sidebarBottomOffset - (leftSidebarBottomBarCornerLoader.active ? Appearance.rounding.screenRounding : 0)
        width: leftSidebarBottomBarCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: leftSidebarBottomBarCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: rightSidebarBottomBarCornerMaskItem
        x: topPanel.width - topPanel.rightSidebarMaskWidth - width
        y: parent.height - topPanel.sidebarBottomOffset - (rightSidebarBottomBarCornerLoader.active ? Appearance.rounding.screenRounding : 0)
        width: rightSidebarBottomBarCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: rightSidebarBottomBarCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: leftSidebarOuterBottomCornerMaskItem
        x: leftSidebar.x
        y: leftSidebar.y + leftSidebar.height
        width: leftSidebarOuterBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: leftSidebarOuterBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    Item {
        id: rightSidebarOuterBottomCornerMaskItem
        x: rightSidebar.x + rightSidebar.width - width
        y: rightSidebar.y + rightSidebar.height
        width: rightSidebarOuterBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
        height: rightSidebarOuterBottomCornerLoader.active ? Appearance.rounding.screenRounding : 0
    }

    // Static mask item for search drop bounds
    Item {
        id: searchDropMaskItem
        visible: searchDropLoader.active && searchDropLoader.item && searchDropLoader.item.isWidgetActive
        x: {
            if (searchDropLoader.item && searchDropLoader.item.isOverviewVisible)
                return 0;
            return searchDropLoader.item ? searchDropLoader.item.x + (searchDropLoader.item.maskItem ? searchDropLoader.item.maskItem.x : 0) : 0;
        }
        y: {
            if (searchDropLoader.item && searchDropLoader.item.isOverviewVisible)
                return 0;
            return searchDropLoader.item ? searchDropLoader.item.y + (searchDropLoader.item.maskItem ? searchDropLoader.item.maskItem.y : 0) : 0;
        }
        width: {
            if (searchDropLoader.item && searchDropLoader.item.isOverviewVisible)
                return topPanel.width;
            return searchDropLoader.item ? (searchDropLoader.item.maskItem ? searchDropLoader.item.maskItem.width : 0) : 0;
        }
        height: {
            if (searchDropLoader.item && searchDropLoader.item.isOverviewVisible)
                return topPanel.height;
            return searchDropLoader.item ? (searchDropLoader.item.maskItem ? searchDropLoader.item.maskItem.height : 0) : 0;
        }
    }

    // Static mask item for OSD drop bounds
    Item {
        id: osdDropMaskItem
        visible: osdDropLoader.active && osdDropLoader.item && osdDropLoader.item.isWidgetActive
        x: osdDropLoader.item ? osdDropLoader.item.x + (osdDropLoader.item.maskItem ? osdDropLoader.item.maskItem.x : 0) : 0
        y: osdDropLoader.item ? osdDropLoader.item.y + (osdDropLoader.item.maskItem ? osdDropLoader.item.maskItem.y : 0) : 0
        width: osdDropLoader.item ? (osdDropLoader.item.maskItem ? osdDropLoader.item.maskItem.width : 0) : 0
        height: osdDropLoader.item ? (osdDropLoader.item.maskItem ? osdDropLoader.item.maskItem.height : 0) : 0
    }

    // Mask region definitions
    mask: Region {
        Region {
            // Bar horizontal
            item: !topPanel.lockTransitionActive && (horizontalBarLoader.item && horizontalBarLoader.item.maskItem) ? horizontalBarLoader.item.maskItem : null
        }
        Region {
            // Bar vertical
            item: !topPanel.lockTransitionActive && (verticalBarLoader.item && verticalBarLoader.item.maskItem) ? verticalBarLoader.item.maskItem : null
        }
        Region {
            // Frame
            regions: !topPanel.lockTransitionActive && frameLoader.item ? [frameLoader.item.frameMask] : []
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarWarmOnMonitor) ? leftSidebarMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarWarmOnMonitor) ? rightSidebarMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarWarmOnMonitor) ? leftSidebarTopCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarWarmOnMonitor) ? leftSidebarOuterTopCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarWarmOnMonitor) ? leftSidebarBottomCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarWarmOnMonitor) ? rightSidebarTopCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarWarmOnMonitor) ? rightSidebarOuterTopCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarWarmOnMonitor) ? rightSidebarBottomCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarWarmOnMonitor) ? leftSidebarBottomBarCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.leftSidebarWarmOnMonitor) ? leftSidebarOuterBottomCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarWarmOnMonitor) ? rightSidebarBottomBarCornerMaskItem : null
        }
        Region {
            item: (!GlobalStates.connectSidebarsSeparate && topPanel.rightSidebarWarmOnMonitor) ? rightSidebarOuterBottomCornerMaskItem : null
        }
        Region {
            // Search drop
            item: searchDropMaskItem
        }
        Region {
            // OSD drop
            item: osdDropMaskItem
        }
    }

    function updateFocusGrab() {
        if (GlobalStates.connectSidebarsSeparate) {
            GlobalFocusGrab.removeDismissable(topPanel);
            return;
        }

        var shouldGrab = false;

        // Left sidebar on this monitor
        if (GlobalStates.sidebarLeftOpen && screen.name === GlobalStates.effectiveLeftMonitor) {
            if (topPanel.policiesOnLeft) {
                if (!GlobalStates.policiesDetached && !GlobalStates.policiesPinned) {
                    shouldGrab = true;
                }
            } else {
                shouldGrab = true;
            }
        }

        // Right sidebar on this monitor
        if (GlobalStates.sidebarRightOpen && screen.name === GlobalStates.effectiveRightMonitor) {
            if (!topPanel.policiesOnLeft) {
                if (!GlobalStates.policiesDetached && !GlobalStates.policiesPinned) {
                    shouldGrab = true;
                }
            } else {
                shouldGrab = true;
            }
        }

        if (shouldGrab) {
            GlobalFocusGrab.addDismissable(topPanel);
        } else {
            GlobalFocusGrab.removeDismissable(topPanel);
        }
    }

    Component.onDestruction: GlobalFocusGrab.removeDismissable(topPanel)

    Connections {
        target: GlobalStates
        function onPoliciesPinnedChanged() {
            topPanel.updateFocusGrab();
        }
        function onPoliciesDetachedChanged() {
            topPanel.updateFocusGrab();
        }
        function onSidebarRightOpenChanged() {
            topPanel.updateFocusGrab();
        }
        function onSidebarLeftOpenChanged() {
            topPanel.updateFocusGrab();
        }
        function onConnectSidebarsSeparateChanged() {
            topPanel.updateFocusGrab();
        }
    }

    Connections {
        target: GlobalFocusGrab
        function onDismissed() {
            // In Float+Connect mode, sidebars handle their own dismissal
            if (GlobalStates.connectSidebarsSeparate)
                return;
            if (GlobalStates.sidebarRightOpen && topPanel.screen.name === GlobalStates.effectiveRightMonitor) {
                if (topPanel.policiesOnLeft)
                    GlobalStates.sidebarRightOpen = false;
                else if (!GlobalStates.policiesDetached && !GlobalStates.policiesPinned)
                    GlobalStates.sidebarRightOpen = false;
            }
            if (GlobalStates.sidebarLeftOpen && topPanel.screen.name === GlobalStates.effectiveLeftMonitor) {
                // A file dialog or the region snip the sidebar itself opened
                // holds it there until it is done.
                if (!topPanel.policiesOnLeft) {
                    GlobalStates.sidebarLeftOpen = false;
                } else if (!GlobalStates.policiesDetached && (!GlobalStates.policiesPinned && GlobalStates.policiesHoldOpen === 0)) {
                    GlobalStates.sidebarLeftOpen = false;
                }
            }
        }
    }

    Item {
        id: keyFocusHandler
        focus: (topPanel.policiesOpenOnMonitor && !GlobalStates.policiesDetached) || (rightSidebarOpenOnMonitor && !(GlobalStates.policiesDetached && topPanel.policiesRenderedOnRight)) || searchOpenOnMonitor
        Keys.onPressed: event => {
            if (event.key === Qt.Key_Escape) {
                GlobalStates.sidebarRightOpen = false;
                if (!GlobalStates.policiesDetached) {
                    GlobalStates.sidebarLeftOpen = false;
                }
                if (searchOpenOnMonitor) {
                    GlobalStates.overviewOpen = false;
                }
                event.accepted = true;
            }

            if ((event.modifiers & Qt.ControlModifier) !== 0 && topPanel.policiesOpenOnMonitor && !GlobalStates.policiesDetached) {
                if (event.key === Qt.Key_O) {
                    topPanel.togglePoliciesExtended();
                } else if (event.key === Qt.Key_D) {
                    topPanel.togglePoliciesDetach();
                } else if (event.key === Qt.Key_P) {
                    topPanel.togglePoliciesPin();
                } else {
                    return;
                }
                event.accepted = true;
            }
        }
    }
}
